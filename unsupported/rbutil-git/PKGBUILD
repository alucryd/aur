# Maintainer: Alucryd <alucryd at gmail dot com>

pkgname=rbutil-git
pkgver=31457
pkgrel=1
pkgdesc="Rockbox Utility git version"
arch=('i686' 'x86_64')
url="http://www.rockbox.org/twiki/bin/view/Main/RockboxUtility"
license=("GPL")
depends=('qt4' 'libusbx' 'speex')
makedepends=('git')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
source=("${pkgname%-*}::git://git.rockbox.org/rockbox"
        'rbutil.desktop')
sha256sums=('SKIP'
            '539285237af16fbc60a7501963e8c2f584e58a31f2613481e6b42d21337b7a25')

pkgver() {
  cd "${srcdir}"/${pkgname%-*}

  git rev-list --count HEAD
}

build() {
  cd "${srcdir}"/${pkgname%-*}/${pkgname%-*}/${pkgname%-*}qt

  lrelease-qt4 rbutilqt.pro
  qmake-qt4
  make
}

package(){
  cd "${srcdir}"/${pkgname%-*}/${pkgname%-*}/${pkgname%-*}qt

  install -dm 755 "${pkgdir}"/usr/{bin,share/pixmaps}
  install -m 644 icons/rockbox-64.png "${pkgdir}"/usr/share/pixmaps/rbutil.png
  install -m 755 RockboxUtility "${pkgdir}"/usr/bin/rbutil
  install -dm 755 "${pkgdir}"/usr/share/applications
  install -m 644 "${srcdir}"/rbutil.desktop "${pkgdir}"/usr/share/applications/rbutil.desktop
}

# vim: ts=2 sw=2 et:
