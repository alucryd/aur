# Maintainer: alucryd <alucryd at gmail dot com>

pkgname=plex-home-theater
pkgver=1.0.1
pkgrel=3
pkgdesc='Plex Home Theater'
arch=('i686' 'x86_64')
url='http://www.plexapp.com/'
license=('GPL2')
depends=('avahi' 'boost-libs' 'curl' 'fontconfig' 'glew' 'java-runtime' 'lame' 'libass' 'libcdio' 'libjpeg-turbo' 'libmad' 'libmicrohttpd' 'libmodplug' 'libmpeg2' 'libpulse' 'libsamplerate' 'libssh' 'libusb-compat' 'libva' 'libxrandr' 'lzo2' 'mesa' 'rtmpdump' 'sdl_image' 'sdl_mixer' 'smbclient' 'sqlite' 'taglib' 'tinyxml' 'yajl')
makedepends=('boost' 'cmake' 'doxygen' 'ftgl' 'java-environment' 'libcec' 'libplist' 'libshairport' 'nasm' 'swig' 'unzip' 'zip')
optdepends=('libplist: AirPlay support'
            'libshairport: AirPlay support'
            'libcec: Pulse-Eight USB-CEC adapter support'
            'pulseaudio: PulseAudio support')
source=("https://github.com/plexinc/plex-home-theater-public/archive/pht-v${pkgver}.tar.gz"
        'plexhometheater.sh')
sha256sums=('91dc03661854655b64764cf4360c7b429d6878f5cadd4985fd0c6e5ba6ccbfe0'
            '15993c2dad0f874253d93f2198320d97e35801962363ad92edbfe936567fcaa3')

build() {
  cd plex-home-theater-public-pht-v${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_BUILD_TYPE='Release' -DCMAKE_INSTALL_PREFIX='/usr' -DENABLE_{AUTOUPDATE='OFF',DUMP_SYMBOLS='OFF',PYTHON='OFF'} -DUSE_INTERNAL_FFMPEG='ON'
  make
}

package() {
  cd plex-home-theater-public-pht-v${pkgver}/build

  make DESTDIR="${pkgdir}" install

# Install additional files
  install -dm 755 "${pkgdir}"/usr/{lib/plexhometheater,share/{applications,pixmaps}}
  install -m 755 "${srcdir}"/plexhometheater.sh "${pkgdir}"/usr/bin/
  install -m 644 ../plex/Resources/plexhometheater.desktop "${pkgdir}"/usr/share/applications/
  install -m 644 ../plex/Resources/plex-icon-256.png "${pkgdir}"/usr/share/pixmaps/plexhometheater.png

# Move things where they should be
  mv "${pkgdir}"/usr/{bin/system,lib/plexhometheater/}
  mv "${pkgdir}"/usr/share/{XBMC,plexhometheater}

# Very ugly workaround
  ln -s ../lib/plexhometheater/system "${pkgdir}"/usr/bin/
}

# vim: ts=2 sw=2 et:
