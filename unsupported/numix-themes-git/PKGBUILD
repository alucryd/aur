# Maintainer: alucryd <alucryd at gmail dot com>
# Contributor: Diego <cdprincipe at gmail dot com>

pkgname=numix-themes-git
pkgver=2.0.r262.52523cf
pkgrel=1
pkgdesc="A flat and light theme with a modern look (GNOME, Openbox, Unity, Xfce)"
arch=('any')
url='http://numixproject.org/'
license=('GPL3')
depends=('gtk-engine-murrine')
makedepends=('git')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
source=("${pkgname%-*}::git+https://github.com/shimmerproject/Numix.git")
md5sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  printf "%s.r%s.%s" "$(git describe --tags | sed 's/^v//; s/-.*//')" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd ${pkgname%-*}

  install -dm 755 "$pkgdir"/usr/share/themes/Numix
  rm -rf .git .gitignore CREDITS LICENSE README.md
  cp -dr --no-preserve=ownership * "$pkgdir"/usr/share/themes/Numix/
}

# vim: ts=2 sw=2 et:
