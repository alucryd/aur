# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: gee <john.ettedgui@gmail.com>

pkgname=nkit
pkgver=1.4
pkgrel=1
pkgdesc='A Nintendo toolkit that can recover and preserve Wii and GameCube disc images'
arch=(any)
url=https://wiki.gbatemp.net/wiki/NKit
license=(MIT)
depends=(mono)
makedepends=(
  git
  mono-msbuild
  nuget
)
source=(
  git+https://github.com/Nanook/NKit.git#commit=31ac76828e6533ba3981eeda79ebfba2f16102af
  nkit-config.patch
  nkit-sharpcompress.patch
)
backup=(etc/nkit/NKit.dll.config)
options=(!emptydirs)
sha256sums=('SKIP'
            '56b7ccf62aee80edbbe6227a24a74fba5bfe6c7b40e4d958dda95ac289235235'
            '2253f5354be07ef419d8537f13327c1911e2ce6c4cb2dbf30c51b2cc1aeffaa3')

prepare() {
  cd NKit

  patch -Np1 -i ../nkit-config.patch
  patch -Np1 -i ../nkit-sharpcompress.patch
}

build() {
  cd NKit

  nuget restore
  msbuild -p:Configuration=Release
  find -name *.pdb -delete
}

package() {
  install -dm 755 "${pkgdir}"/{etc/nkit/Dats/{GameTdb,{Custom,Redump}/{GameCube,Wii}},usr/{bin,lib/nkit}}
  for d in ConvertToISO ConvertToNKit NKitExtractionApp NKitProcessingApp RecoverToISO RecoverToNKit RecoveryExtract; do
    cp -dr --no-preserve=ownership NKit/$d/bin/Release/* "${pkgdir}"/usr/lib/nkit/
    echo "mono /usr/lib/nkit/$d.exe \"\$@\"" > "${pkgdir}"/usr/bin/$d
    chmod +x "${pkgdir}"/usr/bin/$d
  done
  mv "${pkgdir}"/usr/lib/nkit/NKit.dll.config "${pkgdir}"/etc/nkit/
  ln -s /etc/nkit/NKit.dll.config "${pkgdir}"/usr/lib/nkit/
  install -Dm 644 NKit/LICENSE -t "${pkgdir}"/usr/share/licenses/nkit/
}

# vim: ts=2 sw=2 et:
