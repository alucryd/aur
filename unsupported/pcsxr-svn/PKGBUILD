# Maintainer: Alucryd <alucryd at gmail dot com>
# Contributor: Infy <eugene dot yudin at gmail dot com>

pkgname=pcsxr-svn
pkgver=83441
pkgrel=1
pkgdesc="A Sony PlayStation emulator based on the PCSX-df Project"
arch=('i686' 'x86_64')
url="http://pcsxr.codeplex.com/"
license=('GPL')
depends=('gtk3' 'libcdio' 'libxv' 'sdl')
makedepends=('intltool' 'mesa' 'subversion')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}" 'pcsx-df')
options=('!libtool')

_svntrunk=https://pcsxr.svn.codeplex.com/svn/pcsxr
_svnmod=pcsxr

build() {
cd "${srcdir}"

# Checkout
  msg "Connecting to SVN server...."

  if [[ -d ${_svnmod}/.svn ]]; then
    (cd ${_svnmod} && svn up -r ${pkgver})
  else
    svn co ${_svntrunk} --config-dir ./ -r ${pkgver} ${_svnmod}
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "${srcdir}"/${_svnmod}-build
  svn export "${srcdir}"/${_svnmod} "${srcdir}"/${_svnmod}-build
  cd "${srcdir}"/${_svnmod}-build

# Build
  ./autogen.sh --prefix=/usr --enable-libcdio --enable-opengl
  make
}

package() {
  cd "${srcdir}"/${_svnmod}-build

# Install
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
