# Maintainer: Alucryd <alucryd at gmail dot com>

pkgname=gnome-shell-extension-windowoverlay-icons-git
pkgver=35
pkgrel=1
pkgdesc="A GNOME Shell extension to add an icon to windows in the overview"
arch=('any')
url="https://github.com/sustmi/gnome-shell-extensions-sustmi"
license=('GPL3')
depends=('gnome-shell')
makedepends=('git')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
install=${pkgname%-*}.install
source=("${pkgname%-*}::git+https://github.com/sustmi/gnome-shell-extensions-sustmi.git")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}"/${pkgname%-*}

  git rev-list --count HEAD
}

package() {
  cd "${srcdir}"/${pkgname%-*}

  mkdir -p "${pkgdir}"/usr/share/{glib-2.0,gnome-shell/extensions}
  cp -dr --no-preserve=ownership windowoverlay-icons "${pkgdir}"/usr/share/gnome-shell/extensions/windowoverlay-icons@sustmidown.centrum.cz
  cd "${pkgdir}"/usr/share/gnome-shell/extensions/windowoverlay-icons@sustmidown.centrum.cz
  rm -rf po schemas/gschemas.compiled
  mv schemas "${pkgdir}"/usr/share/glib-2.0/
  mv locale "${pkgdir}"/usr/share/

# Fix permissions
  find "${pkgdir}" -type d -exec chmod 755 {} +
  find "${pkgdir}" -type f -exec chmod 644 {} +
}

# vim: ts=2 sw=2 et:
