# Maintainer: alucryd <alucryd at gmail dot dom>

pkgname=numix-circle-icon-theme-git
pkgver=0.r104.bbec939
pkgrel=1
pkgdesc='Circle icon theme from the Numix project'
arch=('any')
url='http://numixproject.org/'
license=('GPL3')
depends=('numix-icon-theme')
makedepends=('git')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
options=('!strip')
install="${pkgname%-*}.install"
source=("${pkgname%-*}::git+https://github.com/numixproject/numix-icon-theme-circle.git")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  printf "0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd ${pkgname%-*}

  install -dm 755 "$pkgdir"/usr/share/icons
  cp -dr --no-preserve=ownership Numix-Circle "$pkgdir"/usr/share/icons/
}

# vim: ts=2 sw=2 et:
