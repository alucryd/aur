# Maintainer: alucryd <alucryd at gmail dot dom>
# Contributor: Diego <cdprincipe at gmail dot com>

pkgname=numix-icon-theme-git
pkgver=0.r39.51e148d
pkgrel=1
pkgdesc="Base icon theme from the Numix project"
arch=('any')
url='http://numixproject.org/'
license=('GPL3')
depends=('gnome-icon-theme')
makedepends=('git')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
options=('!strip')
install="${pkgname%-*}.install"
source=("git+https://github.com/numixproject/${pkgname%-*}.git")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  printf "0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd ${pkgname%-*}

  install -dm 755 "$pkgdir"/usr/share/icons
  cp -dr --no-preserve='ownership' Numix "$pkgdir"/usr/share/icons/
}

# vim: ts=2 sw=2 et:
