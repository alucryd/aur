# Maintainer: alucryd <alucryd at gmail dot com>
# Contributor: Jan Alexander Steffens <jan dot steffens at gmail dot com>
# Contributor: trya <tryagainprod at gmail dot com>

pkgname=gnome-settings-daemon-nopulse
pkgver=3.8.3
pkgrel=1
pkgdesc="The GNOME Settings daemon without PulseAudio"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gnome-desktop' 'ibus' 'libcanberra' 'libnotify' 'libpulse' 'librsvg' 'libwacom' 'upower')
makedepends=('docbook-xsl' 'intltool' 'libxslt' 'xf86-input-wacom')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
options=('!emptydirs' '!libtool')
install=${pkgname%-*}.install
url="http://www.gnome.org"
groups=('gnome')
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname%-*}/${pkgver%.*}/${pkgname%-*}-${pkgver}.tar.xz"
        'git-fixes.patch')
sha256sums=('00e7f46c9a6fb8b148255adfcdd26bc5f2c1d393b8dfbac69da100c8dcfa1b6c'
            '30b0a42df9ea0c6c18ed51cb8f150008e4b5ab624eb420f0251d08ab2503c330')

prepare() {
  cd ${pkgname%-*}-${pkgver}

  patch -Np1 -i ../git-fixes.patch
}

build() {
  cd ${pkgname%-*}-${pkgver}

  aclocal
  autoconf
  automake --add-missing

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/${pkgname%-*} --disable-static

  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd ${pkgname%-*}-${pkgver}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
