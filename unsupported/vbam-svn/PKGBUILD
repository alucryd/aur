# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>

pkgbase=vbam-svn
pkgname=('vbam-cli-svn' 'vbam-gtk-svn' 'vbam-wx-svn')
pkgver=1.8.0.r1231
pkgrel=1
arch=('i686' 'x86_64')
url='http://vba-m.com'
license=('GPL2')
makedepends=('cairo' 'chrpath' 'cmake' 'desktop-file-utils' 'ffmpeg-compat' 'gtkglextmm'
             'imagemagick' 'openal' 'subversion' 'wxgtk2.8' 'zip')
[[ $CARCH == i686 ]] && makedepends+=('nasm')
source=("vbam::svn://svn.code.sf.net/p/vbam/code/trunk")
sha256sums=('SKIP')

pkgver() {
  cd vbam

  printf "1.8.0.r%s" "$(svnversion)"
}

prepare() {
  cd vbam

  sed 's/wxrc/wxrc-2.8/g' -i {po/update_pot.sh,src/wx/CMakeLists.txt}
  sed 's/fn, fnbuf/fn/' -i src/wx/panel.cpp
}

build() {
  cd vbam

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  export PKG_CONFIG_PATH='/usr/lib/ffmpeg-compat/pkgconfig'

  if [[ $CARCH == i686 ]]; then
    cmake .. -DCMAKE_INSTALL_PREFIX='/usr' \
             -DCMAKE_EXE_LINKER_FLAGS='-L/usr/lib/ffmpeg-compat' \
             -DENABLE_{FFMPEG,GTK,WX}='TRUE' \
             -DENABLE_ASM_{CORE,SCALERS}='TRUE' \
             -DwxWidgets_CONFIG_EXECUTABLE='/usr/bin/wx-config-2.8' \
             -DwxWidgets_wxrc_EXECUTABLE='/usr/bin/wxrc-2.8'
  else
    cmake .. -DCMAKE_INSTALL_PREFIX='/usr' \
             -DCMAKE_EXE_LINKER_FLAGS='-L/usr/lib/ffmpeg-compat' \
             -DENABLE_{FFMPEG,GTK,WX}='TRUE' \
             -DwxWidgets_CONFIG_EXECUTABLE='/usr/bin/wx-config-2.8' \
             -DwxWidgets_wxrc_EXECUTABLE='/usr/bin/wxrc-2.8'
  fi
  make
}

package_vbam-cli-svn() {
pkgdesc="Nintendo GameBoy Advance emulator - CLI version"
depends=('glu' 'libpng' 'libsm' 'mesa' 'sdl')
conflicts=('vbam-cli')
backup=('etc/vbam.cfg')

  cd vbam/build

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin/{g,wx}vbam,share}
}

package_vbam-gtk-svn() {
pkgdesc="Nintendo GameBoy Advance emulator - GTK+ version"
depends=('desktop-file-utils' 'gtkglextmm' 'sdl')
conflicts=('vbam-gtk' 'vbam-wx' 'vbam-wx-svn')
install='vbam-gtk.install'

  cd vbam/build

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{etc,usr/bin/vbam}
  find "${pkgdir}" -type f -regex .*wxvbam.* -exec rm {} +
}

package_vbam-wx-svn() {
pkgdesc="Nintendo GameBoy Advance emulator - wxWidgets version"
depends=('glu' 'ffmpeg-compat' 'wxgtk2.8')
conflicts=('vbam-gtk' 'vbam-gtk-svn' 'vbam-wx')
options=('!emptydirs')
install='vbam-wx.install'

  cd vbam/build

  make DESTDIR="${pkgdir}" install
  chrpath -d "${pkgdir}"/usr/bin/wxvbam
  rm -rf "${pkgdir}"/{etc,usr/{bin,share}/vbam}
  find "${pkgdir}" -type f -regex .*gvbam.* -exec rm {} +
}

# vim: ts=2 sw=2 et:
