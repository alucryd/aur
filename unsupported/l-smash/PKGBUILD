# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=l-smash
pkgver=1.11.7
pkgrel=1
pkgdesc='A simple tool for MP4'
arch=('i686' 'x86_64')
url='https://github.com/l-smash/l-smash'
license=('custom')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('58ad53ae211c8352fed189047db44c0cd9de67aa4694e343539ad9d94a92e88a')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix='/usr' --enable-shared --disable-static
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m 644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/
}

# vim: ts=2 sw=2 et:
