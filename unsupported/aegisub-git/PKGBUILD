# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=aegisub-git
pkgver=3.2.1.r46.3d22853
pkgrel=1
pkgdesc='A general-purpose subtitle editor with ASS/SSA support'
arch=('i686' 'x86_64')
url='http://www.aegisub.org'
license=('GPL' 'BSD')
depends=('boost-libs' 'desktop-file-utils' 'ffms2' 'fftw' 'hunspell' 'wxgtk')
makedepends=('boost' 'git' 'intltool' 'mesa')
optdepends=('libpulse: Pulseaudio backend'
            'openal: OpenAL backend'
            'oss: OSS backend'
            'portaudio: Portaudio backend')
provides=('aegisub')
conflicts=('aegisub')
install='aegisub.install'
source=('aegisub::git+https://github.com/Aegisub/Aegisub.git')
sha256sums=('SKIP')

pkgver() {
  cd aegisub

  _tag='v3.2.1'

  printf "%s.r%s.%s" "${_tag#v}" "$(git rev-list --count ${_tag}..HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd aegisub

  ./autogen.sh
}

build () {
  cd aegisub

  ./configure --prefix='/usr'
  make
}

package() {
  cd aegisub

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/usr/share/licenses/aegisub-git
  install -m 644 LICENCE "${pkgdir}"/usr/share/licenses/aegisub-git/
}

# vim: ts=2 sw=2 et:
