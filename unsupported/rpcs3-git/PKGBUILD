# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=rpcs3-git
pkgver=0.0.9.r22.8d847d6f1
pkgrel=1
pkgdesc='A Sony PlayStation 3 emulator'
arch=(x86_64)
url=https://github.com/RPCS3/rpcs3
license=(GPL2)
depends=(
  alsa-lib
  glew
  glu
  libavcodec.so
  libavutil.so
  libevdev
  libgl
  libice
  libncursesw.so
  libpng
  libpulse
  libsm
  libswscale.so
  libx11
  libxext
  openal
  qt5-base
  qt5-declarative
  sdl2
  vulkan-icd-loader
  zlib
)
makedepends=(
  cereal
  cmake
  git
  libglvnd
  python
  vulkan-validation-layers
)
provides=(rpcs3)
conflicts=(rpcs3)
options=(!emptydirs)
source=(
  git+https://github.com/RPCS3/rpcs3.git
  rpcs3-common::git+https://github.com/RPCS3/common.git
  rpcs3-hidapi::git+https://github.com/RPCS3/hidapi.git
  rpcs3-libusb::git+https://github.com/RPCS3/libusb.git
  rpcs3-llvm::git+https://github.com/RPCS3/llvm-mirror.git
  git+https://github.com/kobalicek/asmjit.git
  git+https://github.com/FNA-XNA/FAudio.git
  git+https://github.com/KhronosGroup/glslang.git
  git+https://github.com/zeux/pugixml.git
  git+https://github.com/tcbrindle/span.git
  git+https://github.com/Cyan4973/xxHash.git
  git+https://github.com/jbeder/yaml-cpp.git
)
sha256sums=(
  SKIP
  SKIP
  SKIP
  SKIP
  SKIP
  SKIP
  SKIP
  SKIP
  SKIP
  SKIP
  SKIP
  SKIP
)

pkgver() {
  cd rpcs3

  echo "$(git describe --tags | sed 's/^v//; s/-/.r/; s/-g/./')"
}

prepare() {
  cd rpcs3

  git submodule init 3rdparty/{FAudio,hidapi,libusb,pugixml,span,xxHash,yaml-cpp} asmjit llvm Vulkan/glslang
  git config submodule.asmjit.url ../asmjit
  git config submodule.glslang.url ../glslang
  git config submodule.FAudio.url ../FAudio
  git config submodule.hidapi.url ../rpcs3-hidapi
  git config submodule.libusb.url ../rpcs3-libusb
  git config submodule.llvm.url ../rpcs3-llvm
  git config submodule.pugixml.url ../pugixml
  git config submodule.span.url ../span
  git config submodule.xxHash ../xxHash
  git config submodule.yaml-cpp ../yaml-cpp
  git submodule update 3rdparty/{FAudio,hidapi,libusb,pugixml,span,xxHash,yaml-cpp} asmjit llvm Vulkan/glslang
}

build() {
  cmake -S rpcs3 -B build \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DUSE_NATIVE_INSTRUCTIONS=OFF \
    -DUSE_SYSTEM_FFMPEG=ON \
    -DUSE_SYSTEM_LIBPNG=ON \
    -DUSE_SYSTEM_ZLIB=ON
  make -C build
}

package() {
  make DESTDIR="${pkgdir}" -C build install
}

# vim: ts=2 sw=2 et:
