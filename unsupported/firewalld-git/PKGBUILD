# Maintainer: alucryd <alucryd at gmail dot com>

pkgname=firewalld-git
pkgver=0.3.8.r975.e77a70c
pkgrel=1
pkgdesc="A firewall daemon with D-BUS interface providing a dynamic firewall"
arch=('any')
url="http://fedorahosted.org/firewalld"
license=('GPL2')
depends=('ebtables' 'gtk3' 'iptables' 'libnotify' 'networkmanager' 'python-slip')
makedepends=('docbook-xsl' 'git' 'intltool')
backup=('etc/conf.d/firewalld'
        'etc/firewalld/firewalld.conf')
provides=('firewalld')
conflicts=('firewalld')
install=${pkgname%-*}.install
source=('git+http://git.fedorahosted.org/git/firewalld.git'
        'firewalld-paths.patch')
sha256sums=('SKIP'
            '3fdf9cd88a959c19ca9e5d8e098cfbcbdb073db6602f6e9b0f2ddb542e5a88f8')

pkgver() {
  cd ${pkgname%-*}

  printf "%s.r%s.%s" "$(git describe --tags | sed 's/^r//; s/_/./g; s/-.*//')" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${pkgname%-*}

  patch -Np1 -i ../firewalld-paths.patch
}

build () {
  cd ${pkgname%-*}

  ./autogen.sh
  ./configure --prefix='/usr' --sysconfdir='/etc' --disable-schemas-compile
  make
}

package() {
  cd ${pkgname%-*}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
