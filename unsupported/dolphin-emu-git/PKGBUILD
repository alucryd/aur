# Maintainer: alucryd <alucryd at gmail dot com>
# Contributor: Lightning <sgsdxzy at gmail dot com>

pkgname=dolphin-emu-git
pkgver=4.0.1.r11313.ff91789
pkgrel=1
pkgdesc="A GameCube / Wii / Triforce emulator"
arch=('i686' 'x86_64')
url="http://www.dolphin-emu.org/"
license=('GPL2')
depends=('bluez-libs' 'ffmpeg' 'glew' 'libao' 'libusbx' 'miniupnpc' 'openal' 'portaudio' 'sdl2' 'soundtouch' 'wxgtk2.9')
makedepends=('cmake' 'git' 'opencl-headers')
optdepends=('pulseaudio: PulseAudio backend')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
options=('!emptydirs')
source=("${pkgname%-*}::git+https://code.google.com/p/dolphin-emu/")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  printf "4.0.1.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd ${pkgname%-*}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX='/usr' -DwxWidgets_CONFIG_EXECUTABLE='/usr/bin/wx-config-2.9' -DwxWidgets_wxrc_EXECUTABLE='/usr/bin/wxrc-2.9'
  make

}

package() {
  cd ${pkgname%-*}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
