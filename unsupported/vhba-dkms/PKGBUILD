# Maintainer: Alucryd <alucryd at gmail dot com>
# Contributor: Gustavo Alvarez <sl1pkn07 at gmail dot com>

pkgname=vhba-dkms
pkgver=20130607
pkgrel=1
pkgdesc="VHBA kernel module sources (DKMS)"
arch=('any')
url="http://cdemu.sourceforge.net/"
license=('GPL2')
depends=('dkms')
optdepends=('linux-headers: build the module against Arch kernel'
            'linux-lts-headers: build the module against LTS Arch kernel')
provides=("vhba-module=${pkgver}")
conflicts=('vhba-module')
install=${pkgname}.install
source=("http://downloads.sourceforge.net/cdemu/vhba-module-${pkgver}.tar.bz2" 'dkms.conf' '60-vhba.rules')
sha256sums=('d2c4919d2904a8f880e2730250f7ed6e3143575d5c4de6b88a97c2228aa2a185'
            'a663db5183d0c7e2d830d33d56aeaaeded2f3d13d72f602bca6c74549c7696ae'
            '554bc9a4c609c77c359a609689cf33e36257bb06d429badb30e5be81640034f1')

package() {
  cd "${srcdir}"/vhba-module-${pkgver}

  install -dm 755 "${pkgdir}"/usr/{lib/udev/rules.d,src/vhba-${pkgver}}
  install -m 644 vhba.c "${pkgdir}"/usr/src/vhba-${pkgver}/vhba.c
  install -m 644 Makefile "${pkgdir}"/usr/src/vhba-${pkgver}/Makefile
  install -m 644 "${srcdir}"/dkms.conf "${pkgdir}"/usr/src/vhba-${pkgver}/dkms.conf
  install -m 644 "${srcdir}"/60-vhba.rules "${pkgdir}"/usr/lib/udev/rules.d/60-vhba.rules
  sed -i "s|@VERSION@|${pkgver}|g" "${pkgdir}"/usr/src/vhba-${pkgver}/dkms.conf
}

# vim: ts=2 sw=2 et:
