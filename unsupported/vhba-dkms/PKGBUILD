# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=vhba-dkms
pkgver=20130607
pkgrel=3
pkgdesc='VHBA kernel module sources (DKMS)'
arch=('any')
url='http://cdemu.sourceforge.net/'
license=('GPL2')
depends=('dkms')
optdepends=('linux-headers: build the module against Arch kernel'
            'linux-lts-headers: build the module against LTS Arch kernel')
provides=("vhba-module=${pkgver}")
conflicts=('vhba-module')
install="${pkgname}.install"
source=("http://downloads.sourceforge.net/cdemu/vhba-module-${pkgver}.tar.bz2"
        'vhba-linux3.15.patch'
        'dkms.conf'
        '60-vhba.rules')
sha256sums=('12cfd1d9ab1c7421ef50e2bedc49f90f58afba6164d1acce14b6a4e6a68cc6a6'
            '6ec6e89ab8076b16e2107eeb597f4020194526d1540eb06ebd43de3bd18e7262'
            'a663db5183d0c7e2d830d33d56aeaaeded2f3d13d72f602bca6c74549c7696ae'
            '3052cb1cadbdf4bfb0b588bb8ed80691940d8dd63dc5502943d597eaf9f40c3b')

prepare() {
  cd vhba-module-${pkgver}

  patch -Np2 -i ../vhba-linux3.15.patch
  sed "s/@VERSION@/${pkgver}/g" -i ../dkms.conf
}

package() {
  cd vhba-module-${pkgver}

  install -dm 755 "${pkgdir}"/usr/{lib/udev/rules.d,src/vhba-${pkgver}}
  install -m 644 {Makefile,vhba.c} "${pkgdir}"/usr/src/vhba-${pkgver}/
  install -m 644 "${srcdir}"/dkms.conf "${pkgdir}"/usr/src/vhba-${pkgver}/
  install -m 644 "${srcdir}"/60-vhba.rules "${pkgdir}"/usr/lib/udev/rules.d/
}

# vim: ts=2 sw=2 et:
