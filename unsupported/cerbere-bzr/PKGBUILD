# Maintainer: Alucryd <alucryd at gmail dot com>

pkgname=cerbere-bzr
pkgver=38
pkgrel=1
pkgdesc="A simple service to relaunch Pantheon applications such as the panel, dock, wallpaper, etc."
arch=('i686' 'x86_64')
url="https://launchpad.net/cerbere"
license=('GPL2')
groups=('pantheon')
depends=('dconf' 'libgee')
makedepends=('bzr' 'cmake' 'vala')
conflicts=('cerbere')
install=${pkgname}.install

_bzrtrunk=lp:cerbere
_bzrmod=cerbere

build() {
	msg "Connecting to Bazaar server...."

	if [ -d $_bzrmod ]; then
		cd $_bzrmod && bzr pull $_bzrtrunk -r $pkgver && cd ..
		msg "The local files are updated."
	else
		bzr branch $_bzrtrunk $_bzrmod -r $pkgver
	fi

	msg "BZR checkout done or server timeout"
	msg "Starting make..."

	cd ${_bzrmod}
	cmake . -DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd "${srcdir}/${_bzrmod}"
	make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
	install -dm 755 "${pkgdir}/etc/xdg/autostart"
	install -m 644 data/cerbere.desktop "${pkgdir}/etc/xdg/autostart/cerbere.desktop"
	echo "OnlyShowIn=GNOME;" >> "${pkgdir}/etc/xdg/autostart/cerbere.desktop"
	echo "AutostartCondition=GNOME3 if-session pantheon" >> "${pkgdir}/etc/xdg/autostart/cerbere.desktop"
}
