# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: yubimusubi <possum+aur@possum.cc>

pkgname=3dstool
pkgver=1.1.1
pkgrel=1
pkgdesc='An all-in-one tool for extracting/creating 3ds roms'
arch=('x86_64')
url='https://github.com/dnasdw/3dstool'
license=('None')
depends=('curl' 'glibc' 'openssl-1.0')
makedepends=('cmake')
source=("3dstool-${pkgver}.tar.gz::https://github.com/dnasdw/3dstool/archive/v${pkgver}.tar.gz"
        '3dstool-paths.patch'
        '3dstool-openssl1.0.patch')
sha256sums=('780ad309e5b2f93594d9a2759e50bf6472846b699977a6a59eb8416457f48fcf'
            'fe16de792ebc5d50115a6759a2d727746f93439d5b2ff4fc2db7ea31998cadac'
            'f6099fde2c1cee1bba111ee112991f475ce29a56b55a17255d80f813dc2f598c')

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build

  cd 3dstool-${pkgver}

  patch -Np1 -i ../3dstool-paths.patch
  patch -Np1 -i ../3dstool-openssl1.0.patch
}

build() {
  cd build

  cmake ../3dstool-${pkgver} \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DUSE_DEP='OFF'
  make
}

package() {
  install -Dm 755 3dstool-${pkgver}/bin/Release/3dstool -t "${pkgdir}"/usr/bin/
  install -Dm 777 3dstool-${pkgver}/bin/{ext_key,ignore_3dstool}.txt -t "${pkgdir}"/usr/share/3dstool/
}

# vim: ts=2 sw=2 et:
