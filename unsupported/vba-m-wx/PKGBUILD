# Maintainer: Alucryd <alucryd at gmail dot com>
# Contributor: Barthalion <aur at bpiotrowski dot pl>

pkgname=vba-m-wx
pkgver=1.8.0.1159
pkgrel=2
pkgdesc="A VBA fork which improves the capatibility and features of the emulator"
arch=('i686' 'x86_64')
url="http://vba-m.com"
license=('GPL')
depends=('ffmpeg' 'hicolor-icon-theme' 'sfml1.6' 'wxgtk')
makedepends=('cmake' 'imagemagick' 'mesa' 'zip')
conflicts=('vba-m-gtk')
install=${pkgname}.install
source=("http://downloads.sourceforge.net/project/vbam/vbam-src/vbam-r1159-src.tar.bz2")
sha256sums=('0c5d53b42eb837af963226dad6d175c355991a2bd809a11dcf7d2c832e89fb52')

build() {
  cd "${srcdir}"

# Fix SFML
  sed -i 's|/usr/include|/usr/include/sfml-1.6|' CMakeScripts/FindSFML.cmake
  sed -i 's|NAMES sfml-${LOWERCOMPONENT}|NAMES sfml-${LOWERCOMPONENT}-1.6|' CMakeScripts/FindSFML.cmake

  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_CXX_FLAGS=" -I/usr/include/sfml-1.6" -DENABLE_WX=ON -DENABLE_FFMPEG=ON -DENABLE_GTK=OFF
  make
}

package() {
  cd "${srcdir}"
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
