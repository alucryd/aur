# Maintainer: Maxime Gauduin <alucryd@gmail.dom>

pkgname=numix-bevel-icon-theme-git
pkgver=0.r3.4321b4c
pkgrel=1
pkgdesc='Bevel icon theme from the Numix project'
arch=('any')
url='http://numixproject.org/'
license=('GPL3')
depends=('numix-icon-theme-git')
makedepends=('git')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
options=('!strip')
install="${pkgname%-*}.install"
source=("${pkgname%-*}::git+https://github.com/numixproject/numix-icon-theme-bevel.git")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  printf "0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd ${pkgname%-*}

  install -dm 755 "$pkgdir"/usr/share/icons
  cp -dr --no-preserve=ownership Numix-Bevel "$pkgdir"/usr/share/icons/
}

# vim: ts=2 sw=2 et:
