# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=pyanimenc-git
pkgver=r11.8c36ed9
pkgrel=1
pkgdesc='Python Transcoding Tools'
arch=('any')
url='https://github.com/alucryd/pyanimenc'
license=('GPL3')
depends=('fdkaac' 'python-lxml' 'x264' 'vapoursynth-plugin-ffms2')
optdepends=('python-gobject: GTK+ GUI'
            'vapoursynth-plugin-flash3kyuu_deband-git: Deband filter'
            'x264-10bit: Hi10 encoding')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
source=("git+${url}.git")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package(){
  cd ${pkgname%-*}

  install -dm 755 "${pkgdir}"/usr/{bin,lib/python3.4/site-packages,share/${pkgname%-*}}
  install -m 755 encaudio encvideo mkchap transcode "${pkgdir}"/usr/bin/
  install -m 644 ${pkgname%-*}.py "${pkgdir}"/usr/lib/python3.4/site-packages/
  install -m 644 *.json "${pkgdir}"/usr/share/${pkgname%-*}/
}

# vim: ts=2 sw=2 et:
