# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: whoami <whoami@systemli.org>

pkgname=vlang
pkgver=0.1.24
pkgrel=1
pkgdesc='Simple, fast, safe language created for developing maintainable software'
arch=(x86_64)
url=https://vlang.io
license=(MIT)
depends=(glibc)
makedepends=(git)
optdepends=(
  glfw
  libfreetype.so
  libcurl.so
)
source=(
  git+https://github.com/vlang/v.git#tag=526f12feff629228785c88f052db91d4fb8510ca
  git+https://github.com/vlang/vc.git#commit=b9ea14b1f384b584b146ee59a8b179a1e2d68299
)
sha256sums=(
  SKIP
  SKIP
)

build() {
  cd v

  cc $CFLAGS -std=gnu11 -w -o v ../vc/v.c $LDFLAGS -lm
  ./v -prod -cflags $CFLAGS -o v v.v
  find tools -name '*.v' -exec ./v -prod -cflags $CFLAGS {} \;
}

package() {
  cd v

  install -dm 755 "${pkgdir}"/usr/{bin,{lib,share}/vlang}
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/vlang/
  install -m 755 v -t "${pkgdir}"/usr/lib/vlang/
  ln -s /usr/lib/vlang/v "${pkgdir}"/usr/bin/v
  cp -dr --no-preserve=ownership vlib thirdparty tools "${pkgdir}"/usr/lib/vlang/
  cp -dr --no-preserve=ownership examples "${pkgdir}"/usr/share/vlang/
}

# vim: ts=2 sw=2 et:
