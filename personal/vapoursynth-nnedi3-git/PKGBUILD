# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=vapoursynth-nnedi3-git
pkgver=1.1
pkgrel=1
pkgdesc="nnedi3 plugin for Vapoursynth"
arch=('x86_64')
url='https://github.com/dubhater/vapoursynth-nnedi3'
license=('GPL')
depends=('vapoursynth')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
makedepends=('git' 'yasm')
source=("git+${url}.git")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  printf "%s" "$(git describe --tags | sed 's/-/.r/; s/-g/./; s/^v//')"
}

build() {
  cd ${pkgname%-*}

  ./autogen.sh
  ./configure --prefix='/usr' --libdir='/usr/lib/vapoursynth'
  make
}

package(){
  cd ${pkgname%-*}

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/usr/share/doc/vapoursynth/nnedi3
  install -m 644 readme.rst "${pkgdir}"/usr/share/doc/vapoursynth/nnedi3
}

# vim: ts=2 sw=2 et:
