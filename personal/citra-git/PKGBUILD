# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>
# Contributor: ceri

pkgbase=citra-git
pkgname=('citra-cli-git' 'citra-qt-git')
pkgver=r7392.83e3e30d0
pkgrel=1
pkgdesc='A Nintendo 3DS emulator'
arch=('x86_64')
url="https://github.com/citra-emu/citra/"
license=('GPL2')
makedepends=('cmake' 'git' 'mesa' 'qt5-base' 'qt5-multimedia' 'sdl2')
source=('git+https://github.com/citra-emu/citra'
        'citra-boost::git+https://github.com/citra-emu/ext-boost.git'
        'git+https://github.com/philsquared/Catch.git'
        'git+https://github.com/zeromq/cppzmq.git'
        'git+https://github.com/weidai11/cryptopp.git'
        'git+https://github.com/kinetiknz/cubeb.git'
        'git+https://github.com/discordapp/discord-rpc.git'
        'git+https://github.com/MerryMage/dynarmic.git'
        'git+https://github.com/lsalzman/enet.git'
        'git+https://github.com/fmtlib/fmt.git'
        'git+https://github.com/svn2github/inih.git'
        'citra-libressl::git+https://github.com/citra-emu/ext-libressl-portable.git'
        'git+https://github.com/zeromq/libzmq.git'
        'git+https://github.com/neobrain/nihstro.git'
        'git+https://github.com/arsenm/sanitizers-cmake.git'
        'citra-soundtouch::git+https://github.com/citra-emu/ext-soundtouch.git'
        'git+https://github.com/herumi/xbyak.git')
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

pkgver() {
  cd citra

  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build

  cd citra

  for submodule in externals/{catch,cppzmq,cryptopp/cryptopp,cubeb,discord-rpc,dynarmic,enet,fmt,inih/inih,libressl,libzmq,nihstro,xbyak}; do
    git submodule init ${submodule}
    git config submodule.${submodule}.url ../${submodule##*/}
    git submodule update
  done
  for submodule in externals/{boost,soundtouch}; do
    git submodule init ${submodule}
    git config submodule.${submodule}.url ../citra-${submodule##*/}
    git submodule update
  done

  cd externals/cubeb

  for submodule in cmake/sanitizers-cmake; do
    git submodule init ${submodule}
    git config submodule.${submodule}.url ../../../${submodule##*/}
    git submodule update
  done

}

build() {
  cd build

  cmake ../citra \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr'
  make
}

package_citra-cli-git() {
  depends=('alsa-lib' 'sdl2')

  install -Dm 755 build/bin/citra -t "${pkgdir}"/usr/bin/
}

package_citra-qt-git() {
  depends=('alsa-lib' 'hicolor-icon-theme' 'qt5-base' 'qt5-multimedia' 'sdl2')

  make DESTDIR="${pkgdir}" -C build install
  rm "${pkgdir}"/usr/bin/citra
}

# vim: ts=2 sw=2 et:
