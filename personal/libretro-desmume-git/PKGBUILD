# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Johnathan Jenkins <twodopeshaggy@gmail.com>
# Contributor: prettyvanilla <prettyvanilla@posteo.at>
# Contributor: almostalive <almostalive2003@gmail.com>

pkgname=libretro-desmume-git
pkgver=r6276.59d93b5b
pkgrel=1
pkgdesc='Nintendo DS core'
arch=('x86_64')
url='https://github.com/libretro/desmume'
license=('GPL2')
groups=('libretro-unstable')
depends=('gcc-libs' 'glibc' 'libretro-core-info' 'libgl')
makedepends=('git')
provides=('libretro-desmume')
conflicts=('libretro-desmume')
source=('libretro-desmume::git+https://github.com/libretro/desmume.git')
sha256sums=('SKIP')

pkgver() {
  cd libretro-desmume

  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  cd libretro-desmume/desmume/src/frontend/libretro

  make -f Makefile.libretro
}

package() {
  cd libretro-desmume/desmume/src/frontend/libretro

  install -Dm 644 desmume_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
