# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=pypy3-hg
pkgver=7.0.0
pkgrel=1
pkgdesc='A Python3 implementation written in Python, JIT enabled'
url=https://pypy.org
arch=(x86_64)
depends=(
  bzip2
  expat
  gdbm
  libffi
  ncurses
  openssl
  zlib
)
makedepends=(
  mercurial
  python2
  python2-pycparser
  sqlite
  tk
)
optdepends=(
  'sqlite: sqlite module'
  'tk: tk module'
)
provides=(pypy3)
conflicts=(pypy3)
options=(!buildflags)
license=(MIT)
source=(hg+https://bitbucket.org/pypy/pypy#branch=release-pypy3.6-v7.x)
sha256sums=(SKIP)

build() {
  cd pypy/pypy/goal

  python2 ../../rpython/bin/rpython -Ojit --shared targetpypystandalone

  # Compile binary modules
  PYTHONPATH=../.. ./pypy3-c ../tool/build_cffi_imports.py
}

package() {
  cd pypy

  # Prepare installation
  python2 pypy/tool/release/package.py --archive-name pypy --targetdir .
  mkdir unpacked
  tar xf pypy.tar.bz2 -C unpacked

  # Install pypy
  cd unpacked
  install -Dm755 pypy/bin/pypy3 "${pkgdir}"/opt/pypy3/bin/pypy3
  install -Dm755 pypy/bin/libpypy3-c.so "${pkgdir}"/opt/pypy3/bin/libpypy3-c.so
  cp -r pypy/include pypy/lib_pypy pypy/lib-python pypy/site-packages "${pkgdir}"/opt/pypy3
  cd ..

  # Install symlinks
  mkdir -p "${pkgdir}"/usr/bin "${pkgdir}"/usr/lib
  ln -s /opt/pypy3/bin/pypy3 "${pkgdir}"/usr/bin/pypy3
  ln -s /opt/pypy3/bin/libpypy3-c.so "${pkgdir}"/usr/lib/libpypy3-c.so

  # Install misc stuff
  install -Dm644 README.rst "${pkgdir}"/opt/pypy3/README.rst
  install -Dm644 LICENSE "${pkgdir}"/opt/pypy3/LICENSE
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/pypy3/LICENSE
}

# vim: ts=2 sw=2 et:
