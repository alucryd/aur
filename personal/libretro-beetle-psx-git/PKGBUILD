# Maintainer: Maxime Gauduin <alucryd@Ã¦rchlinux.org>
# Contributor: Mikuro Kagamine <mikurok@forgecrushing.com>

pkgbase=libretro-beetle-psx-git
pkgname=('libretro-beetle-psx-git' 'libretro-beetle-psx-hw-git')
pkgver=r2051.68b6683
pkgrel=1
pkgdesc='Sony playStation core'
arch=('x86_64')
url="https://github.com/libretro/beetle-psx-libretro"
license=('GPL2')
groups=('libretro-unstable')
depends=('libretro-core-info')
makedepends=('git' 'libglvnd' 'mesa')
source=('libretro-beetle-psx::git+https://github.com/libretro/beetle-psx-libretro.git')
sha256sums=('SKIP')

pkgver() {
  cd libretro-beetle-psx

  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  make -C libretro-beetle-psx
  make HAVE_HW=1 -C libretro-beetle-psx
}

package_libretro-beetle-psx-git() {
  provides=('libretro-beetle-psx')
  conflicts=('libretro-beetle-psx')

  install -Dm 644 libretro-beetle-psx/mednafen_psx_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

package_libretro-beetle-psx-hw-git() {
  depends+=('libgl')
  provides=('libretro-beetle-psx-hw')
  conflicts=('libretro-beetle-psx-hw')

  install -Dm 644 libretro-beetle-psx/mednafen_psx_hw_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
