# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Lauri Niskanen <ape@ape3000.com>

pkgname=libretro-gambatte-git
pkgver=r819.6f3bde1
pkgrel=1
pkgdesc='Nintendo Game Boy/Game Boy Color core'
arch=('x86_64')
url='https://github.com/libretro/gambatte-libretro'
license=('GPL2')
groups=('libretro-unstable')
depends=('libretro-core-info')
makedepends=('git')
provides=('libretro-gambatte')
conflicts=('libretro-gambatte')
source=('libretro-gambatte::git+https://github.com/libretro/gambatte-libretro.git')
sha256sums=('SKIP')

pkgver() {
  cd libretro-gambatte

  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  make -C libretro-gambatte -f Makefile.libretro
}

package() {
  install -Dm 644 libretro-gambatte/gambatte_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
