# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Thomas Schneider <maxmusterm@gmail.com>

pkgname=libretro-mupen64plus-git
pkgver=r695.563565d
pkgrel=1
pkgdesc='Nintendo 64 core'
arch=('x86_64')
url='https://github.com/libretro/mupen64plus-libretro'
license=('GPL2')
groups=('libretro-unstable')
depends=('libgl' 'libpng' 'libretro-core-info' 'zlib')
makedepends=('git' 'libglvnd' 'mesa')
provides=('libretro-mupen64plus')
conflicts=('libretro-mupen64plus')
source=('libretro-mupen64plus::git+https://github.com/libretro/mupen64plus-libretro.git')
sha256sums=('SKIP')

pkgver() {
  cd libretro-mupen64plus

  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  make WITH_DYNAREC='x86_64' -C libretro-mupen64plus
}

package() {
  install -Dm 644 libretro-mupen64plus/mupen64plus_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
