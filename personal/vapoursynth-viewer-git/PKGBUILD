# Maintainer: sl1pkn07 <sl1pkn07@gmail.com>

pkgname=vapoursynth-viewer-git
pkgver=r33.78954da
pkgrel=1
pkgdesc='A simple program for previewing VapourSynth scripts'
arch=('x86_64')
url='https://github.com/dubhater/vapoursynth-viewer'
license=('custom:WTFPL')
depends=('vapoursynth' 'qt4')
makedepends=('git')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
source=('git+https://github.com/dubhater/vapoursynth-viewer.git')
md5sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd ${pkgname%-*}

  qmake-qt4
  make
}

package() {
  cd ${pkgname%-*}

  install -dm 755 "${pkgdir}"/usr/{bin,share/licenses/${pkgname}}
  install -m 755 vsviewer "${pkgdir}"/usr/bin/vsviewer
  install -m 644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/
}

# vim: ts=2 sw=2 et:
