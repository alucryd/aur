# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Mike Swanson <mikeonthecomputer@gmail.com>
# Contributor: Themaister <maister@archlinux.us>

pkgname=retroarch-git
pkgver=1.7.8.4.r197.b9a08520ff
pkgrel=1
pkgdesc='Reference frontend for the libretro API'
arch=(x86_64)
url=http://www.libretro.com/
license=(GPL)
groups=(libretro-unstable)
depends=(
  alsa-lib
  libass.so
  libavcodec.so
  libavformat.so
  libavutil.so
  libdrm
  libfreetype.so
  libgl
  libpulse
  libswresample.so
  libswscale.so
  libudev.so
  libusb-1.0.so
  mbedtls
  mesa
  miniupnpc
  openal
  sdl2
  v4l-utils
  zlib
)
makedepends=(
  git
  libx11
  libxcb
  libxext
  libxinerama
  libxkbcommon
  libxrandr
  libxv
  libxxf86vm
  qt5-base
  vulkan-icd-loader
  wayland
  wayland-protocols
)
optdepends=(
  'libretro-bsnes: Super Nintendo Entertainment System cores'
  'libretro-desmume: Nintendo DS core'
  'libretro-gambatte: Nintendo Game Boy/Game Boy Color core'
  'libretro-genesis-plus: Sega Master System/Genesis/Game Gear/CD/32X core'
  'libretro-mgba: Nintendo Game Boy Advance core'
  'libretro-mupen64plus: Nintendo 64 core'
  'libretro-nestopia: Nintendo Entertainment System core'
  'libretro-pcsx-rearmed: Sony PlayStation core'
  'libretro-ppsspp: Sony PlayStation Portable core'
  'libretro-snes9x: Super Nintendo Entertainment System core'
  'libretro-yabause: Sega Saturn core'
  'libretro-overlays: Collection of overlays'
  'libretro-shaders-cg: Collection of Cg shaders'
  'libretro-shaders-glsl: Collection of GLSL shaders'
  'libretro-shaders-slang: Collection of Vulkan shaders'
  'libxinerama: X11 support'
  'libxrandr: X11 support'
  'python: retroarch-cg2glsl'
  'qt5-base: WIMP GUI'
  'retroarch-assets-xmb: XMB menu assets'
  'wayland: Wayland support'
)
provides=(retroarch)
conflicts=(retroarch)
backup=(etc/retroarch.cfg)
source=(
  retroarch::git+https://github.com/libretro/RetroArch.git
  retroarch-paths.patch
)
sha256sums=(
  SKIP
  b2e60c6d40722015ec313163544f6820d68df13be181f8529378dace2bf28b6e
)

pkgver() {
  cd retroarch

  git describe --tags | sed 's/v//; s/-/.r/; s/-g/./'
}

prepare() {
  cd retroarch

  patch -Np1 -i ../retroarch-paths.patch
}

build() {
  cd retroarch

  ./configure \
    --prefix='/usr' \
    --disable-builtinflac \
    --disable-builtinmbedtls \
    --disable-builtinminiupnpc \
    --disable-builtinzlib \
    --disable-cg \
    --disable-jack \
    --disable-oss \
    --disable-sdl \
    --enable-egl\
    --enable-zlib
  make
  make -C libretro-common/audio/dsp_filters
  make -C gfx/video_filters
}

package() {
  cd retroarch

  make DESTDIR="${pkgdir}" install
  install -Dm 644 libretro-common/audio/dsp_filters/*.{dsp,so} -t "${pkgdir}"/usr/lib/retroarch/filters/audio/
  install -Dm 644 gfx/video_filters/*.{filt,so} -t "${pkgdir}"/usr/lib/retroarch/filters/video/
}

# vim: ts=2 sw=2 et:
