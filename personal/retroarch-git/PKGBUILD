# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Mike Swanson <mikeonthecomputer@gmail.com>
# Contributor: Themaister <maister@archlinux.us>

pkgname=retroarch-git
pkgver=1.7.6.r517.1414bffa8e
pkgrel=1
pkgdesc='Reference frontend for the libretro API'
arch=(x86_64)
url=http://www.libretro.com/
license=(GPL)
groups=(libretro-unstable)
depends=(
  alsa-lib
  dbus
  libass.so
  libavcodec.so
  libavformat.so
  libavutil.so
  libdrm
  libfreetype.so
  libgl
  libpulse
  libswresample.so
  libswscale.so
  libudev.so
  libusb-1.0.so
  libx11
  libxcb
  libxext
  libxinerama
  libxkbcommon
  libxrandr
  libxv
  libxxf86vm
  mesa
  openal
  sdl2
  v4l-utils
  wayland
  zlib
)
makedepends=(
  git
  vulkan-icd-loader
)
optdepends=(
  'libretro-bsnes: Super Nintendo Entertainment System cores'
  'libretro-desmume: Nintendo DS core'
  'libretro-gambatte: Nintendo Game Boy/Game Boy Color core'
  'libretro-genesis-plus: Sega Master System/Genesis/Game Gear/CD/32X core'
  'libretro-mgba: Nintendo Game Boy Advance core'
  'libretro-mupen64plus: Nintendo 64 core'
  'libretro-nestopia: Nintendo Entertainment System core'
  'libretro-pcsx-rearmed: Sony PlayStation core'
  'libretro-ppsspp: Sony PlayStation Portable core'
  'libretro-snes9x: Super Nintendo Entertainment System core'
  'libretro-yabause: Sega Saturn core'
  'libretro-overlays: Collection of overlays'
  'libretro-shaders-cg: Collection of Cg shaders'
  'libretro-shaders-glsl: Collection of GLSL shaders'
  'libretro-shaders-slang: Collection of Vulkan shaders'
)
provides=(retroarch)
conflicts=(retroarch)
backup=(etc/retroarch.cfg)
source=(
  retroarch::git+https://github.com/libretro/RetroArch.git
  retroarch-paths.patch
)
sha256sums=(
  SKIP
  b2e60c6d40722015ec313163544f6820d68df13be181f8529378dace2bf28b6e
)

pkgver() {
  cd retroarch

  git describe --tags | sed 's/v//; s/-/.r/; s/-g/./'
}

prepare() {
  cd retroarch

  patch -Np1 -i ../retroarch-paths.patch
}

build() {
  cd retroarch

  ./configure \
    --prefix='/usr' \
    --disable-cg \
    --disable-jack \
    --disable-oss
  make
}

package() {
  cd retroarch

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
