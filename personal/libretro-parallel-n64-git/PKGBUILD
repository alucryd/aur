# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Thomas Schneider <maxmusterm@gmail.com>

pkgname=libretro-parallel-n64-git
pkgver=r5068.fbf39c86
pkgrel=1
pkgdesc='Nintendo 64 core'
arch=('x86_64')
url='https://github.com/libretro/parallel-n64'
license=('GPL2')
groups=('libretro-unstable')
depends=('gcc-libs' 'glibc' 'libgl' 'libretro-core-info')
makedepends=('git')
provides=('libretro-parallel-n64')
conflicts=('libretro-parallel-n64')
source=('libretro-parallel-n64::git+https://github.com/libretro/parallel-n64.git')
sha256sums=('SKIP')

pkgver() {
  cd libretro-parallel-n64

  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  cd libretro-parallel-n64

  make WITH_DYNAREC='x86_64'
}

package() {
  cd libretro-parallel-n64

  install -Dm 644 parallel_n64_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
