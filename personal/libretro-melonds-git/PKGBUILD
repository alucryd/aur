# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-melonds-git
pkgver=r557.ee2f3e3
pkgrel=1
pkgdesc='Nintendo DS core'
arch=('x86_64')
url='https://github.com/libretro/melonDS'
license=('GPL2')
groups=('libretro-unstable')
depends=('gcc-libs' 'glibc' 'libretro-core-info')
makedepends=('git')
provides=('libretro-melonds')
conflicts=('libretro-melonds')
source=('libretro-melonds::git+https://github.com/libretro/melonDS.git')
sha256sums=('SKIP')

pkgver() {
  cd libretro-melonds

  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  cd libretro-melonds

  export LDFLAGS="$LDFLAGS -lpthread"

  make
}

package() {
  cd libretro-melonds

  install -Dm 644 melonds_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
