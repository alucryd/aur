# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: gaelic <gaelic@luchmhor.net>

pkgname=paper-icon-theme-git
pkgver=r827.ab51c46f
pkgrel=1
pkgdesc='A modern freedesktop icon theme whose design is based around the use of bold colours and simple geometric shapes to compose icons'
arch=('any')
url='https://github.com/snwh/paper-icon-theme'
license=('custom: CC BY-SA 4.0')
makedepends=('git' 'meson')
provides=('paper-icon-theme')
conflicts=('paper-icon-theme')
source=('git+https://github.com/snwh/paper-icon-theme.git')
sha256sums=('SKIP')

pkgver() {
  cd paper-icon-theme

  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  arch-meson paper-icon-theme build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm 644 paper-icon-theme/LICENSE -t "${pkgdir}"/usr/share/licenses/paper-icon-theme-git/
}

# vim: ts=2 sw=2 et:
