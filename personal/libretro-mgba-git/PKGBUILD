# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Duck Hunt <vaporeon@tfwno.gf>

pkgname=libretro-mgba-git
pkgver=r6157.9c8a1a59
pkgrel=1
pkgdesc='Nintendo Game Boy Advance core'
arch=('x86_64')
url='https://github.com/libretro/mgba'
license=('custom:MPL2')
groups=('libretro-unstable')
depends=('libretro-core-info')
makedepends=('git')
provides=('libretro-mgba')
conflicts=('libretro-mgba')
source=('libretro-mgba::git+https://github.com/libretro/mgba.git')
sha256sums=('SKIP')

pkgver() {
  cd libretro-mgba

  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  make -C libretro-mgba -f Makefile.libretro
}

package() {
  install -Dm 644 libretro-mgba/mgba_libretro.so -t "${pkgdir}"/usr/lib/libretro/
  install -Dm 644 libretro-mgba/LICENSE -t "${pkgdir}"/usr/share/licenses/libretro-mgba-git/
}

# vim: ts=2 sw=2 et:
