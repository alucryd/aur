# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Karl-Felix Glatzer <karl.glatzer@gmx.de>
# Contributor: kfgz <kfgz@interia.pl>
# Contributor: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>

pkgname=mingw-w64-x265-hg
pkgver=1.7.r51.a7bf7a150a70
pkgrel=1
pkgdesc='CLI tools for encoding H265/HEVC video streams.'
arch=('x86_64')
url='https://bitbucket.org/multicoreware/x265'
license=('GPL')
makedepends=('mercurial' 'mingw-w64-cmake' 'mingw-w64-crt' 'mingw-w64-gcc'
             'mingw-w64-pkg-config' 'yasm')
options=('!buildflags' '!strip')
source=('hg+https://bitbucket.org/multicoreware/x265')
sha256sums=('SKIP')

pkgver() {
  cd x265

  echo "$(hg log -r. --template "{latesttag}").r$(hg log -r. --template "{latesttagdistance}").$(hg log -r. --template "{node|short}")"
}

prepare() {
  cp -r x265 x265-10bit
}

build() {
  cd x265

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  x86_64-w64-mingw32-cmake ../source \
    -DCMAKE_BUILD_TYPE='Release' \
    -DHIGH_BIT_DEPTH='FALSE' \
    -DSTATIC_LINK_CRT='TRUE' \
    -DENABLE_SHARED='TRUE'
  make

  cd ../../x265-10bit

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  x86_64-w64-mingw32-cmake ../source \
    -DCMAKE_BUILD_TYPE='Release' \
    -DHIGH_BIT_DEPTH='TRUE' \
    -DSTATIC_LINK_CRT='TRUE' \
    -DENABLE_SHARED='FALSE'
  make
}

package() {
  cd x265/build

  cp libx265.dll "${pkgdir}"/libx265_main.dll

  cd ../../x265-10bit/build

  cp x265.exe "${pkgdir}"/

  x86_64-w64-mingw32-strip "${pkgdir}"/*
}

# vim: ts=2 sw=2 et:
