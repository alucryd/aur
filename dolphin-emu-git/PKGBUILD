# Maintainer: Alucryd <alucryd at gmail dot com>
# Contributor: Lightning <sgsdxzy at gmail dot com>

pkgname=dolphin-emu-git
pkgver=20130203
pkgrel=1
pkgdesc="A GameCube / Wii / Triforce emulator - GIT version"
arch=('i686' 'x86_64')
url="http://www.dolphin-emu.org/"
license=('GPL2')
depends=('bluez' 'lzo2' 'nvidia-cg-toolkit' 'sdl' 'soil' 'soundtouch' 'wxgtk2.9')
makedepends=('cmake' 'git' 'glproto' 'opencl-headers')
optdepends=('alsa-lib: ALSA backend'
            'libpulse: PulseAudio backend'
            'libao: AOsound backend'
            'openal: OpenAL backend'
            'portaudio: mic support'
            'ffmpeg: AVI dump')
provides=('dolphin-emu')
conflicts=('dolphin-emu' 'dolphin-emu-svn-bin' 'dolphin-emu-svn')

_gitroot=http://code.google.com/p/dolphin-emu/
_gitname=dolphin-emu

build() {
  cd "$srcdir"

  msg "Connecting to GIT server...."
  if [[ -d ${_gitname} ]] ; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone ${_gitroot} ${_gitname}
    cd ${_gitname}
  fi
  msg "GIT checkout done or server timeout"

  msg "Starting build..."
  if [[ -d build ]] ; then
    rm -rf build
  fi
  mkdir build
  cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DwxWidgets_CONFIG_EXECUTABLE=/usr/bin/wx-config-2.9 -DwxWidgets_wxrc_EXECUTABLE=/usr/bin/wxrc-2.9
  make

}
package() {
  cd "${srcdir}"/${_gitname}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
