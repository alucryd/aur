# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>

pkgname=cerbere
pkgver=0.2.2
pkgrel=1
pkgdesc='The Pantheon Watchdog'
arch=('i686' 'x86_64')
url='https://github.com/elementary/cerbere'
license=('GPL2')
groups=('pantheon')
depends=('dconf' 'libgee')
makedepends=('cmake' 'vala' 'git')
# https://github.com/elementary/cerbere/commit/07d836acce7898af449f80a2c8694f3b471fee1d
# https://launchpad.net/cerbere/0.x/0.2.2
_commit='07d836acce7898af449f80a2c8694f3b471fee1d'
source=("git+https://github.com/elementary/${pkgname}.git#commit=${_commit}")
sha512sums=('SKIP')

prepare() {
  cd ${pkgname}
  sed "s/'wingpanel','plank','slingshot-launcher --silent'/'gala','wingpanel','plank','slingshot-launcher --silent'/" -i org.pantheon.cerbere.gschema.xml
}

build() {
  cd ${pkgname}
  mkdir -p build && cd build

  cmake .. \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DGSETTINGS_COMPILE='OFF'
  make
}

package() {
  cd ${pkgname}/build
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
