# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=switchboard-plug-about-bzr
pkgver=r151
pkgrel=1
pkgdesc='About plug for Switchboard'
arch=('i686' 'x86_64')
url='https://launchpad.net/switchboard-plug-about'
license=('GPL3')
depends=('switchboard-bzr')
makedepends=('bzr' 'cmake' 'vala')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
source=('bzr+lp:switchboard-plug-about'
        'about-archlinux.patch')
sha256sums=('SKIP'
            '2c68f1307b939a19a1cccebf75af387c32dfe68cf95f97ed6174fc29c56a2462')

pkgver() {
  cd ${pkgname%-*}

  printf "r%s" "$(bzr revno)"
}

prepare() {
  cd ${pkgname%-*}

#  patch -Np1 -i ../about-archlinux.patch
  sed 's|${CMAKE_INSTALL_FULL_LIBDIR}|${CMAKE_INSTALL_PREFIX}/lib|g' -i "$(grep -rl CMAKE_INSTALL_FULL_LIBDIR)"
}

build() {
  cd ${pkgname%-*}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX='/usr'
  make
}

package() {
  cd ${pkgname%-*}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
