# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>

pkgname=pantheon-photos-bzr
pkgver=r2506
pkgrel=1
pkgdesc='The Pantheon Photos Manager'
arch=('i686' 'x86_64')
url='https://launchpad.net/pantheon-photos'
license=('GPL3')
depends=('desktop-file-utils' 'json-glib' 'libgee' 'libgexiv2' 'libgphoto2' 'libraw' 'rest' 'webkitgtk')
makedepends=('bzr' 'vala')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
install="${pkgname%-*}.install"
source=('bzr+lp:photos-manager')
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  printf "r%s" "$(bzr revno)"
}

build() {
  cd ${pkgname%-*}

  ./configure --prefix='/usr' --libexec='/usr/lib/shotwell' --disable-{{desktop,icon}-update,schemas-compile}
  make
}

package() {
  cd ${pkgname%-*}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
