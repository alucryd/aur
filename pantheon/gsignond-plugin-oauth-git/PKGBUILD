# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=gsignond-plugin-oauth-git
pkgver=r115.d471ceb
pkgrel=1
pkgdesc='OAuth plugin for gSSO'
arch=('x86_64')
url='https://01.org/gsso'
license=('LGPL2.1')
depends=('glib2' 'glibc' 'gnutls' 'json-glib' 'libsoup'
         'libgsignond-common.so')
makedepends=('check' 'git' 'gobject-introspection' 'gsignond' 'meson')
provides=('gsignond-plugin-oauth')
conflicts=('gsignond-plugin-oauth')
source=('gsignond-plugin-oauth::git+https://gitlab.com/accounts-sso/gsignond-plugin-oa.git')
sha256sums=('SKIP')

pkgver() {
  cd gsignond-plugin-oauth

  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
  sed -i "/subdir('docs')/d" gsignond-plugin-oauth/meson.build
}

build() {
  arch-meson gsignond-plugin-oauth build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
