# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=gsignond-plugin-oauth-git
pkgver=r68.0130aac
pkgrel=1
pkgdesc='OAuth plugin for gSSO'
arch=('i686' 'x86_64')
url='https://01.org/gsso'
license=('LGPL2.1')
depends=('gsignond' 'json-glib' 'libsoup')
makedepends=('git' 'gobject-introspection' 'gtk-doc')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
source=("${pkgname%-*}::git+https://code.google.com/p/accounts-sso.gsignond-plugin-oa/")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd ${pkgname%-*}

  mkdir -p m4
  gtkdocize
  aclocal
  autoheader
  libtoolize --copy --force
  autoconf
  automake --add-missing --copy
  ./configure --prefix='/usr' --enable-gtk-doc
  make
}

package() {
  cd ${pkgname%-*}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
