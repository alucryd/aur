# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>

pkgname=pantheon-terminal-bzr
pkgver=r643
pkgrel=1
pkgdesc='The Pantheon Terminal Emulator'
arch=('i686' 'x86_64')
url='https://launchpad.net/pantheon-terminal'
license=('GPL3')
depends=('desktop-file-utils' 'granite-bzr' 'libnotify' 'vte3')
makedepends=('bzr' 'cmake' 'vala')
provides=('pantheon-terminal')
conflicts=('pantheon-terminal')
install='pantheon-terminal.install'
source=('pantheon-terminal::bzr+lp:~stevenvandenbrandenstift/pantheon-terminal/fix-1375346')
sha256sums=('SKIP')

pkgver() {
  cd pantheon-terminal

  printf "r%s" "$(bzr revno)"
}

build() {
  cd pantheon-terminal

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX='/usr' -DGSETTINGS_COMPILE='OFF'
  make
}

package() {
  cd pantheon-terminal/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
