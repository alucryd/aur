# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>

pkgname=wingpanel-indicator-network
pkgver=2.1.1
pkgrel=1
pkgdesc='Network indicator for Wingpanel'
arch=('i686' 'x86_64')
url='https://github.com/elementary/wingpanel-indicator-network'
license=('GPL3')
groups=('pantheon')
depends=('glib2' 'glibc' 'gtk3' 'libnm-glib'
         'libgranite.so' 'libwingpanel-2.0.so' 'nm-connection-editor')
makedepends=('cmake' 'nm-connection-editor' 'vala' 'wingpanel')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/elementary/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('7c83aa1c9d71c80826b92a636117850bc4805db6940fceb4622249c4135154a4b6ebb490b5ef440e41fdf01597ecc785f17e8cd493fb321c4ca4ef5fd439d784')

build() {
  cd ${pkgname}-${pkgver}
  mkdir -p build
  cd build

  cmake .. \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib'
  make
}

package() {
  cd ${pkgname}-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
