# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-print-git
pkgver=r27.c92021f
pkgrel=1
pkgdesc='Simple shim for printing support via Contractor'
arch=(x86_64)
url=https://github.com/elementary/pantheon-print
license=(GPL3)
depends=(
  cairo
  contractor
  glib2
  glibc
  gtk3
  pango
)
makedepends=(
  contractor-git
  git
  meson
  vala
)
provides=(pantheon-print)
conflicts=(pantheon-print)
source=(git+https://github.com/elementary/pantheon-print.git)
sha256sums=(SKIP)

pkgver() {
  cd pantheon-print

  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  arch-meson pantheon-print build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
