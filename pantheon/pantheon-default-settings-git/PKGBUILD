# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgbase=pantheon-default-settings-git
pkgname=('elementary-dpms-helper-git' 'pantheon-default-settings-git')
pkgver=r340.09ae797
pkgrel=1
arch=('any')
url='https://github.com/elementary/default-settings'
license=('GPL')
groups=('pantheon-unstable')
makedepends=('git')
source=('pantheon-default-settings::git+https://github.com/elementary/default-settings.git#branch=meson'
        '0001-arch-tweaks.patch')
sha256sums=('SKIP'
            '778bd3cf1c3c2f52e01eb19c6aeca4fa42b29b3244af9d4651f056c5ac3bbecf')

pkgver() {
  cd pantheon-default-settings

  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {

  cd pantheon-default-settings

  patch -Np1 -i ../0001-arch-tweaks.patch
}

build() {
  cd pantheon-default-settings

  arch-meson build
  ninja -C build
}

package_elementary-dpms-helper-git() {
  pkgdesc='DPMS helper for Pantheon'
  depends=('gnome-settings-daemon')
  provides=('elementary-dpms-helper' 'pantheon-dpms-helper-git')
  conflicts=('elementary-dpms-helper' 'pantheon-dpms-helper')

  cd pantheon-default-settings/dpms

  install -Dm 644 elementary-dpms-helper.desktop -t "${pkgdir}"/etc/xdg/autostart/
  install -Dm 755 elementary-dpms-helper -t "${pkgdir}"/usr/bin/
  install -Dm 644 org.pantheon.dpms.gschema.xml -t "${pkgdir}"/usr/share/glib-2.0/schemas/
}

package_pantheon-default-settings-git() {
  pkgdesc='Default settings for Pantheon'
  depends=('elementary-icon-theme-git' 'gtk-theme-elementary-git'
           'elementary-wallpapers-git' 'ttf-droid' 'ttf-opensans')
  provides=('pantheon-default-settings')
  conflicts=('pantheon-default-settings')

  cd pantheon-default-settings
  DESTDIR=${pkgdir} ninja -C build install

  install -Dm 644 debian/elementary-default-settings.gsettings-override "${pkgdir}"/usr/share/glib-2.0/schemas/25_pantheon-default-settings.gschema.override
  rm $pkgdir/etc/lightdm/lightdm.conf
  rm -rf $pkgdir/usr/share/cups/data

}

# vim: ts=2 sw=2 et:
