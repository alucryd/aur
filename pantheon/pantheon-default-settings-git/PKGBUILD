# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgbase=pantheon-default-settings-git
pkgname=('elementary-dpms-helper-git' 'pantheon-default-settings-git')
pkgver=r308.d2722d3
pkgrel=1
arch=('any')
url='https://github.com/elementary/default-settings'
license=('GPL')
groups=('pantheon-unstable')
makedepends=('git')
source=('pantheon-default-settings::git+https://github.com/elementary/default-settings.git'
        'arch-tweaks.patch')
sha256sums=('SKIP'
            '431f0fc13d562806db6724837c58c90cb9225d0fb53a85c7d687be4e2acef8bb')

pkgver() {
  cd pantheon-default-settings

  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build

  cd pantheon-default-settings

  patch -Np1 -i ../arch-tweaks.patch
}

build() {
  cd build

  cmake ../pantheon-default-settings \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DGSETTINGS_COMPILE='OFF'
  make
}

package_elementary-dpms-helper-git() {
  pkgdesc='DPMS helper for Pantheon'
  depends=('gnome-settings-daemon')
  provides=('elementary-dpms-helper')
  conflicts=('elementary-dpms-helper')

  cd pantheon-default-settings/dpms

  install -Dm 644 elementary-dpms-helper.desktop -t "${pkgdir}"/etc/xdg/autostart/
  install -Dm 755 elementary-dpms-helper -t "${pkgdir}"/usr/bin/
  install -Dm 644 org.pantheon.dpms.gschema.xml -t "${pkgdir}"/usr/share/glib-2.0/schemas/
}

package_pantheon-default-settings-git() {
  pkgdesc='Default settings for Pantheon'
  depends=('elementary-icon-theme-git' 'gtk-theme-elementary-git'
           'elementary-wallpapers-git' 'ttf-droid' 'ttf-opensans')
  provides=('pantheon-default-settings')
  conflicts=('pantheon-default-settings')

  cd pantheon-default-settings

  install -Dm 644 debian/elementary-default-settings.gsettings-override "${pkgdir}"/usr/share/glib-2.0/schemas/25_pantheon-default-settings.gschema.override
  install -Dm 644 policykit.desktop -t "${pkgdir}"/usr/share/applications/
  install -Dm 644 settings.ini -t "${pkgdir}"/etc/gtk-3.0/
  install -dm 755 "${pkgdir}"/etc/skel/.config
  cp -dr --no-preserve='ownership' plank "${pkgdir}"/etc/skel/.config/
  cp -dr --no-preserve='ownership' profile.d "${pkgdir}"/etc/
}

# vim: ts=2 sw=2 et:
