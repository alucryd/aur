# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=evolution-data-server-poa-bzr
pkgver=r23
pkgrel=1
pkgdesc='POA to EDS Bridge'
arch=('i686' 'x86_64')
url='https://launchpad.net/poa-eds-bridge'
license=('GPL3')
depends=('evolution-data-server' 'json-glib' 'libaccounts-glib' 'libgee'
         'libgsignon-glib' 'rest')
makedepends=('bzr' 'cmake' 'vala')
provides=('evolution-data-server-poa')
conflicts=('evolution-data-server-poa')
source=('evolution-data-server-poa::bzr+lp:poa-eds-bridge')
sha256sums=('SKIP')

pkgver() {
  cd evolution-data-server-poa

  echo "r$(bzr revno)"
}

build() {
  cd evolution-data-server-poa

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib'
  make
}

package() {
  cd evolution-data-server-poa/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
