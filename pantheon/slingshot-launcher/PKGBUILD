# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=slingshot-launcher
pkgver=0.7.6.1
pkgrel=1
pkgdesc='The Pantheon Application Launcher'
arch=('i686' 'x86_64')
url='https://launchpad.net/slingshot'
license=('GPL3')
depends=('gnome-menus2' 'granite' 'libwnck3' 'libzeitgeist')
makedepends=('cmake' 'vala')
install="${pkgname}.install"
source=("${url}/0.x/${pkgver}/+download/${pkgname}-${pkgver}.tgz")
sha256sums=('fa7766281b323e0f86db64afc24d0fd9e8765773da54eb703aaa507cddc0ebcb')

build() {
  cd ${pkgname}-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX='/usr' -DCMAKE_EXE_LINKER_FLAGS='-lm' -D{GSETTINGS_COMPILE,USE_UNITY}='OFF'
  make
}

package() {
  cd ${pkgname}-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
