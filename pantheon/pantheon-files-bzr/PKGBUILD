# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>

pkgname=pantheon-files-bzr
pkgver=r1424
pkgrel=1
pkgdesc='The Pantheon File Manager'
arch=('i686' 'x86_64')
url='https://launchpad.net/pantheon-files'
license=('GPL3')
depends=('desktop-file-utils' 'gconf' 'granite' 'libnotify')
makedepends=('bzr' 'cmake' 'gnome-common' 'sqlite3' 'vala')
optdepends=('contractor-bzr: Various context menu entries'
            'pantheon-files-plugin-dropbox-bzr: Dropbox integration'
            'tumbler: Thumbnails generation')
provides=("${pkgname%-*}")
conflicts=('marlin-bin' 'marlin-bzr' "${pkgname%-*}")
install="${pkgname%-*}.install"
source=('bzr+lp:pantheon-files')
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  printf "r%s" "$(bzr revno)"
}

build() {
  cd ${pkgname%-*}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX='/usr' -DGSETTINGS_COMPILE='OFF'
  make
}

package() {
  cd ${pkgname%-*}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
