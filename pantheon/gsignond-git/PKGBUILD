# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic@gmail.com>

pkgname=gsignond-git
pkgver=1.0.6.r26.20b9f80
pkgrel=1
pkgdesc='gSSO glib daemon'
arch=('x86_64')
url='https://01.org/gsso'
license=('LGPL2.1')
depends=('glib2' 'glibc' 'sqlite')
makedepends=('git' 'gobject-introspection' 'gtk-doc' 'meson' 'vala')
provides=('gsignond' 'libgsignond-common.so')
conflicts=('gsignond')
backup=('etc/gsignond.conf')
source=("git+https://gitlab.com/accounts-sso/gsignond.git")
sha256sums=('SKIP')

pkgver() {
  cd gsignond

  git describe --tags | sed 's/-/.r/; s/-g/./'
}

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd build

  arch-meson ../gsignond
  ninja
}

package() {
  cd build

  DESTDIR="${pkgdir}" ninja install
}

# vim: ts=2 sw=2 et:
