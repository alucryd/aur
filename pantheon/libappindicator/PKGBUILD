# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: bitwave <aur@oomlu.de>
# Contributor: willemw <willemw12@gmail.com>
# Contributor: Balló György <ballogyor+arch@gmail.com>

pkgbase=libappindicator
pkgname=('libappindicator-gtk2' 'libappindicator-gtk3' 'libappindicator-sharp')
pkgver=12.10.0
pkgrel=3
pkgdesc='Allow applications to export a menu into the Unity Menu bar'
arch=('i686' 'x86_64')
url="https://launchpad.net/${pkgbase}"
license=('LGPL')
makedepends=('dbus-glib' 'gobject-introspection' 'gtk-sharp-2'
             'libdbusmenu-gtk2' 'libdbusmenu-gtk3' 'libindicator-gtk2'
             'libindicator-gtk3' 'perl-xml-libxml' 'pygtk' 'vala')
options=('!emptydirs')
source=("http://launchpad.net/${pkgbase}/${pkgver%.*}/${pkgver}/+download/${pkgbase}-${pkgver}.tar.gz")
sha256sums=('d5907c1f98084acf28fd19593cb70672caa0ca1cf82d747ba6f4830d4cc3b49f')

prepare() {
  sed 's|/cli/|/mono/|' -i ${pkgbase}-${pkgver}/bindings/mono/{appindicator-sharp-0.1.pc.in,Makefile.in}
  cp -r ${pkgbase}-${pkgver} ${pkgbase}-gtk2-${pkgver}
}

build() {
  cd ${pkgbase}-${pkgver}

  ./configure --prefix='/usr' --sysconfdir='/etc' --localstatedir='/var' \
              --disable-{static,tests} --with-gtk='3'
  make -j1

  cd ../${pkgbase}-gtk2-${pkgver}

  ./configure --prefix='/usr' --sysconfdir='/etc' --localstatedir='/var' \
              --disable-{static,tests}
  make -j1
}

package_libappindicator-gtk2() {
  depends=('libdbusmenu-gtk2' 'libindicator-gtk2')
  provides=('libappindicator')
  conflicts=('libappindicator')

  cd ${pkgbase}-gtk2-${pkgver}

  make -j1 DESTDIR="${pkgdir}" install
  make -j1 -C bindings/mono DESTDIR="${pkgdir}" uninstall
  rm -rf "${pkgdir}"/usr/share
}

package_libappindicator-gtk3() {
  depends=('libdbusmenu-gtk3' 'libindicator-gtk3')
  provides=('libappindicator3')
  conflicts=('libappindicator3')

  cd ${pkgbase}-${pkgver}

  make -j1 DESTDIR="${pkgdir}" install
  make -j1 -C bindings/mono DESTDIR="${pkgdir}" uninstall
  rm -rf "${pkgdir}"/usr/share
}

package_libappindicator-sharp() {
arch=('any')

  cd ${pkgbase}-${pkgver}

  make -j1 -C bindings/mono DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/share
}

# vim: ts=2 sw=2 et:
