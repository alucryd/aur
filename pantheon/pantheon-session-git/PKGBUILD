# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-session-git
pkgver=r138.231ab3b
pkgrel=1
pkgdesc='Session settings for Pantheon'
arch=(any)
url=https://github.com/elementary/session-settings
license=(GPL3)
groups=(pantheon-unstable)
depends=(
  dconf
  gala
  gnome-keyring
  gnome-session
  gnome-settings-daemon
  pantheon-applications-menu
  pantheon-dpms-helper
  plank
  wingpanel
  xdg-user-dirs-gtk
)
makedepends=(
  git
  meson
)
optdepends=(pantheon-default-settings)
provides=(pantheon-session)
conflicts=(pantheon-session)
source=(pantheon-session::git+https://github.com/elementary/session-settings.git#branch=focal)
sha256sums=(SKIP)

pkgver() {
  cd pantheon-session

  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  arch-meson pantheon-session build \
    -Dpatched-ubuntu-autostarts=false \
    -Dfallback-session=gnome
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
