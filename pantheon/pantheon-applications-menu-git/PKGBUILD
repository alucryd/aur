# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-applications-menu-git
pkgver=2.4.1.r18.7f3ca21
pkgrel=1
pkgdesc='The Pantheon Application Menu'
arch=('x86_64')
url='https://github.com/elementary/applications-menu'
license=('GPL3')
groups=('pantheon-unstable')
depends=('appstream' 'gdk-pixbuf2' 'glib2' 'gnome-menus' 'gtk3' 'json-glib'
         'libgee' 'libsoup' 'plank' 'zeitgeist'
         'libgranite.so' 'libswitchboard-2.0.so' 'libwingpanel-2.0.so')
makedepends=('cmake' 'git' 'granite-git' 'switchboard-git' 'vala'
             'wingpanel-git')
provides=('pantheon-applications-menu')
conflicts=('pantheon-applications-menu')
source=('pantheon-applications-menu::git+https://github.com/elementary/applications-menu.git')
sha256sums=('SKIP')

pkgver() {
  cd pantheon-applications-menu

  git describe --tags | sed 's/-/.r/; s/-g/./'
}

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd build

  cmake ../pantheon-applications-menu \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib' \
    -DGSETTINGS_COMPILE='OFF' \
    -DUSE_UNITY='OFF'
  make
}

package() {
  make DESTDIR="${pkgdir}" -C build install
}

# vim: ts=2 sw=2 et:
