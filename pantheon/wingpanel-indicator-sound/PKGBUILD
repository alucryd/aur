# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>

pkgname=wingpanel-indicator-sound
pkgver=2.0.5
pkgrel=2
pkgdesc='Wingpanel Sound Indicator'
arch=('i686' 'x86_64')
url='https://github.com/elementary/wingpanel-indicator-sound'
license=('GPL3')
groups=('pantheon')
depends=('cairo' 'gdk-pixbuf2' 'glib2' 'glibc' 'gtk3' 'libcanberra' 'libgee' 'gtk2'
         'libnotify' 'libpulse' 'libgranite.so' 'libwingpanel-2.0.so')
makedepends=('cmake' 'intltool' 'vala' 'wingpanel')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/elementary/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('5661897e48be39173f1dc7c59a65e88ae7e65979550c2121f01cbd6b94ff3f72dadc1728ae0b3b1e5bc3837593b3ab889f29febe243178dc940667e430dfd903')

build() {
  cd ${pkgname}-${pkgver}
  mkdir -p build
  cd build

  cmake .. \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib'
  make
}

package() {
  cd ${pkgname}-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}


# vim: ts=2 sw=2 et:
