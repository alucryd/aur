# Maintainer: Alucryd <alucryd at gmail dot com>

pkgname=launchpad-integration-gtk3
pkgver=0.1.56.2
pkgrel=1
pkgdesc="A library to integrate launchpad urls into the menus"
arch=('i686' 'x86_64')
url="https://launchpad.net/launchpad-integration"
license=('GPL3')
depends=('hicolor-icon-theme' 'gtk3' 'pygtk')
makedepends=('bzr' 'gobject-introspection')
options=('!libtool')
install=${pkgname%-*}.install
source=("bzr+lp:${pkgname%-*}#revision=189")
sha256sums=('SKIP')

build() {
  cd "${srcdir}"/${pkgname%-*}

  export PYTHON=python2
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/${pkgname%-*} --disable-static --with-gtk=3
  make
}

package() {
  cd "${srcdir}"/${pkgname%-*}

  make DESTDIR="${pkgdir}" install

# Fix permissions
  chmod 644 "${pkgdir}"/usr/share/launchpad-integration/launchpadintegration/*
}

# vim: ts=2 sw=2 et:
