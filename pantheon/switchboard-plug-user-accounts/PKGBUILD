# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>

pkgname=switchboard-plug-useraccounts
pkgver=0.1.5
pkgrel=1
pkgdesc='User Accounts plug for Switchboard'
arch=('i686' 'x86_64')
url='https://github.com/elementary/switchboard-plug-useraccounts'
license=('GPL3')
groups=('pantheon')
depends=('accountsservice' 'cairo' 'gdk-pixbuf2' 'glib2' 'glibc'
         'gnome-desktop' 'gtk3' 'libgee' 'libpwquality' 'polkit'
         'libgranite.so' 'libswitchboard-2.0.so' 'gobject-introspection')
makedepends=('cmake' 'switchboard' 'vala')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/elementary/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('21b11bfaa2ed7031d6bdedde713c1c480b45ce28b7e57bf80cb52e611b4da89a1cb7609a03b521e1c80334b69dd2e3d45174d667cbce92ce7930731c57e5da98')

build() {
  cd ${pkgname}-${pkgver}
  mkdir -p build
  cd build

  cmake .. \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib'
  make
}

package() {
  cd ${pkgname}-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
