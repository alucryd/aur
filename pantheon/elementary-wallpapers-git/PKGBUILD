# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=elementary-wallpapers-git
pkgver=r124.47b25c2
pkgrel=1
pkgdesc='Collection of wallpapers for elementary OS'
arch=(any)
url=https://github.com/elementary/wallpapers
license=(GPL3)
groups=(pantheon-unstable)
makedepends=(
  git
  meson
)
provides=(elementary-wallpapers)
conflicts=(elementary-wallpapers)
source=(
  elementary-wallpapers::git+https://github.com/elementary/wallpapers.git
  elementary-wallpapers-install-path.patch
)
sha256sums=('SKIP'
            'c6d599f649a35ae386b422c2fa061d441495999f664da31f22021d6dcea133aa')

pkgver() {
  cd elementary-wallpapers

  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
  cd elementary-wallpapers

  patch -Np1 -i ../elementary-wallpapers-install-path.patch
}

build() {
  arch-meson elementary-wallpapers build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
