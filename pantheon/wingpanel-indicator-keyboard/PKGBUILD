# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>

pkgname=wingpanel-indicator-keyboard
pkgver=2.0.2
pkgrel=1
pkgdesc='Wingpanel Keyboard Indicator'
arch=('i686' 'x86_64')
url='https://github.com/elementary/wingpanel-indicator-keyboard'
license=('GPL3')
groups=('pantheon')
depends=('glib2' 'glibc' 'gtk3' 'libxml2'
         'libwingpanel-2.0.so')
makedepends=('cmake' 'vala' 'wingpanel')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/elementary/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('36cf7b1aafe07c2fbc6ef34fa053b4559f3e7789933006c7eebaad951e87742e87cffc1ad16602e3c3b1fb315d7e9a7a3859efc1a9fd0a9b749a0d9adfc6fff0')

build() {
  cd ${pkgname}-${pkgver}
  mkdir -p build
  cd build

  cmake .. \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib'
  make
}

package() {
  cd ${pkgname}-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
