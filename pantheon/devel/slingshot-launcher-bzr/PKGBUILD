# Maintainer: Alucryd <alucryd at gmail dot com>
# Contributor: flamelab <panosfilip@gmail.com>

pkgname=slingshot-launcher-bzr
pkgver=351
pkgrel=1
pkgdesc="Slingshot application launcher"
arch=('i686' 'x86_64')
url="https://launchpad.net/slingshot"
license=('GPL3')
depends=('gnome-menus2' 'granite-bzr' 'libunity' 'libwnck3' 'libzeitgeist')
makedepends=('bzr' 'cmake' 'vala')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
install=${pkgname%-*}.install
source=("bzr+lp:${pkgname%%-*}")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}"/${pkgname%%-*}

  bzr revno
}

build() {
  cd "${srcdir}"/${pkgname%%-*}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DGSETTINGS_COMPILE=OFF
  make
}

package() {
  cd "${srcdir}"/${pkgname%%-*}/build

  make DESTDIR="${pkgdir}" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install
}

# vim: ts=2 sw=2 et:
