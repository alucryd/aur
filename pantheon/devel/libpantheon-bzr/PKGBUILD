# Maintainer: Alucryd <alucryd at gmail dot com>

pkgname=libpantheon-bzr
pkgver=23
pkgrel=1
pkgdesc="Library for the Pantheon Desktop Environment"
arch=('i686' 'x86_64')
url="https://launchpad.net/libpantheon"
license=('GPL3')
depends=('gtk3')
makedepends=('bzr' 'cmake' 'gobject-introspection' 'vala>=0.16')
provides=('libpantheon')
conflicts=('libpantheon')

_bzrtrunk=https://code.launchpad.net/~elementary-pantheon/libpantheon/trunk
_bzrmod=libpantheon

build() {
 cd "${srcdir}"

# Pull
  msg "Connecting to Bazaar server...."

  if [[ -d ${_bzrmod} ]]; then
    cd ${_bzrmod} && bzr --no-plugins pull ${_bzrtrunk} -r ${pkgver}
    msg "The local files are updated."
  else
    bzr --no-plugins branch ${_bzrtrunk} ${_bzrmod} -q -r ${pkgver}
  fi

  msg "Bazaar checkout done or server timeout"
  msg "Starting build..."

  rm -rf "${srcdir}"/${_bzrmod}-build
  cp -r "${srcdir}"/${_bzrmod} "${srcdir}"/$_bzrmod-build
  cd "${srcdir}"/${_bzrmod}-build

# Build
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/${_bzrmod}-build/build

# Install
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
