# Maintainer: Alucryd <alucryd at gmail dot com>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>

pkgname=scratch-text-editor-bzr
pkgver=1041
pkgrel=1
pkgdesc="A text editor written in Vala"
arch=('i686' 'x86_64')
url="https://launchpad.net/scratch"
license=('GPL3')
groups=('pantheon')
depends=('gtksourceview3' 'libpeas' 'pantheon-files-bzr' 'python2-chardet' 'vte3' 'webkitgtk3' 'libzeitgeist')
makedepends=('bzr' 'cmake' 'gobject-introspection' 'vala')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
install=${pkgname%-*}.install
source=("bzr+lp:${pkgname%%-*}")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}"/${pkgname%%-*}

  bzr revno
}

prepare() {
  cd "${srcdir}"/${pkgname%%-*}

  sed -i 's/gtkspell-3.0/gtkspell3-3.0/' plugins/spell/CMakeLists.txt
  sed -i '/add_subdirectory (spell)/d' plugins/CMakeLists.txt
  sed -i 's|^#!.*python$|#!/usr/bin/python2|' $(grep -rl '^#!.*python')
}

build() {
  cd "${srcdir}"/${pkgname%%-*}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DGSETTINGS_COMPILE=OFF
  make
}

package() {
  cd "${srcdir}"/${pkgname%%-*}/build

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
  chmod +x "${pkgdir}"/usr/share/scratch/scripts/*.py
}

# vim: ts=2 sw=2 et:
