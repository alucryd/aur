# Maintainer: Alucryd <alucryd at gmail dot com>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>

pkgname=audience-bzr
pkgver=271
pkgrel=1
pkgdesc="A modern media player"
arch=('i686' 'x86_64')
url="https://launchpad.net/audience"
license=('GPL3')
depends=('clutter-gtk' 'desktop-file-utils' 'granite-bzr' 'gst-plugins-base-libs')
makedepends=('bzr' 'clutter-gst' 'cmake' 'vala')
install=${pkgname%-*}.install
source=("bzr+lp:${pkgname%-*}")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}"/${pkgname%-*}

  bzr revno
}

build() {
  cd "${srcdir}"/${pkgname%-*}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DGSETTINGS_COMPILE=OFF
  make
}

package() {
  cd "${srcdir}"/${pkgname%-*}/build

  make DESTDIR="${pkgdir}" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install
}

# vim: ts=2 sw=2 et:
