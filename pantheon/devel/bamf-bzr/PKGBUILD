# Maintainer: Alucryd <alucryd at gmail dot com>
# Contributor: dorkfish <dork dot fish dot wat at gmail dot com>

pkgname=bamf-bzr
pkgver=553
pkgrel=1
pkgdesc="Application matching framework"
arch=('i686' 'x86_64')
url="https://launchpad.net/bamf"
license=('GPL')
depends=('dbus-glib' 'libgtop' 'libwnck3' 'python2-lxml')
makedepends=('bzr' 'gnome-common' 'gobject-introspection' 'gtk-doc' 'vala')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
options=('!libtool')
source=("bzr+lp:${pkgname%-*}")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}"/${pkgname%-*}

  bzr revno
}

prepare() {
  cd "${srcdir}"/${pkgname%-*}

  sed -i 's|^#!.*python$|#!/usr/bin/python2|' tests/gtester2xunit.py
}

build() {
  cd "${srcdir}"/${pkgname%-*}

  export PYTHON=/usr/bin/python2
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/${pkgname%-*} --disable-{static,webapps}
  make
}

package() {
  cd "${srcdir}"/${pkgname%-*}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
