# Maintainer: Alucryd <alucryd at gmail dot com>
# Contributor: dorkfish <dork dot fish dot wat at gmail dot com>

pkgname=bamf-bzr
pkgver=534
pkgrel=1
pkgdesc="Application matching framework"
arch=('i686' 'x86_64')
url="https://launchpad.net/bamf"
license=('GPL')
depends=('dbus-glib' 'libgtop' 'libwnck3')
makedepends=('bzr' 'gnome-common' 'gobject-introspection' 'gtk-doc' 'vala')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
options=('!libtool')
source=("bzr+lp:${pkgname%-*}"
        'bamf-matcher.patch')
sha256sums=('SKIP'
            'c6e338adf43992cd9838e33b871cf9f554a21a3eb7e6afe0588c68c5f7c71967')

pkgver() {
  cd "${srcdir}"/${pkgname%-*}

  bzr revno
}

prepare() {
  cd "${srcdir}"/${pkgname%-*}

  patch -Np1 -i ../bamf-matcher.patch
}

build() {
  cd "${srcdir}"/${pkgname%-*}

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/${pkgname%-*} --disable-{static,webapps}
  make
}

package() {
  cd "${srcdir}"/${pkgname%-*}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
