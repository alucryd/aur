# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=elementary-icon-theme-git
pkgver=5.0.3.r4.8333483b
pkgrel=1
pkgdesc='Named, vector icons for elementary OS'
arch=('any')
url='https://github.com/elementary/icons'
license=('GPL3')
groups=('pantheon-unstable')
depends=('hicolor-icon-theme')
makedepends=('git' 'meson')
provides=('elementary-icon-theme')
conflicts=('elementary-icon-theme')
options=('!emptydirs')
source=('elementary-icon-theme::git+https://github.com/elementary/icons.git')
sha256sums=('SKIP')

pkgver() {
  cd elementary-icon-theme

  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  arch-meson elementary-icon-theme build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
  rm "${pkgdir}"/.VolumeIcon*
}

# vim: ts=2 sw=2 et:

