# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-dpms-helper-git
pkgver=r8.e2547fd
pkgrel=1
pkgdesc='DPMS helper for Pantheon'
arch=('any')
url='https://github.com/elementary/dpms-helper'
license=('GPL')
groups=('pantheon-unstable')
depends=('bash' 'dconf')
makedepends=('desktop-file-utils' 'git' 'meson')
provides=('elementary-dpms-helper' 'pantheon-dpms-helper')
conflicts=('elementary-dpms-helper' 'pantheon-dpms-helper')
source=('pantheon-dpms-helper::git+https://github.com/elementary/dpms-helper.git')
sha256sums=('SKIP')

pkgver() {
  cd pantheon-dpms-helper

  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd build

  arch-meson ../pantheon-dpms-helper
  ninja
}

package() {
  cd build

  DESTDIR="${pkgdir}" ninja install
}

# vim: ts=2 sw=2 et:
