# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=wingpanel
pkgver=0.2.5
pkgrel=3
pkgdesc='The Pantheon Top Panel'
arch=('i686' 'x86_64')
url="https://launchpad.net/${pkgname}"
license=('GPL3')
depends=('granite' 'libindicator-gtk3')
optdepends=('indicator-datetime: Display date and time'
            'indicator-pantheon-session-bzr: Pantheon session indicator'
            'indicator-power: Battery idicator')
makedepends=('cmake' 'vala')
install="${pkgname}.install"
source=("${url}/0.2.x/${pkgver}/+download/${pkgname}-${pkgver}.tgz")
sha256sums=('c4bc2b78b24d2f943215b68637d6b4ef23ca9f845fab6d6fef9e3896851bb144')

build() {
  cd ${pkgname}-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_BUILD_TYPE='Release' -DCMAKE_INSTALL_PREFIX='/usr' -DGSETTINGS_COMPILE='OFF'
  make
}

package() {
  cd ${pkgname}-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
