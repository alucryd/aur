# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel
pkgver=2.0.3
pkgrel=2
pkgdesc='The Pantheon Top Panel'
arch=('i686' 'x86_64')
url='https://github.com/elementary/wingpanel'
license=('GPL3')
groups=('pantheon')
depends=('clutter' 'cogl' 'glib2' 'glibc' 'gtk3' 'libgee'
         'libgala.so' 'libgranite.so' 'mutter')
makedepends=('cmake' 'gala' 'vala')
provides=('libwingpanel-2.0.so')
source=("wingpanel-${pkgver}.tar.gz::https://github.com/elementary/wingpanel/archive/${pkgver}.tar.gz")
sha512sums=('4f42ed52b6e95f2db2cf1bf02817102d76814de5a8d16cfbcd4d0551190726d82c658ae0918d021dd95a10462168810398da88f678735d8a93c0ccdcfa1ecc71')

prepare() {
  cd wingpanel-${pkgver}
  mkdir -p build
}

build() {
  cd wingpanel-${pkgver}/build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib' \
    -DGSETTINGS_COMPILE='OFF'
  make
}

package() {
  cd wingpanel-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
