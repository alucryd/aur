# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

gitname=calendar
auxname=("pantheon-${gitname}")
pkgname=("${auxname}-git")
pkgver=r1770.7ec952ee
pkgrel=1
pkgdesc='The Pantheon Calendar'
arch=('x86_64')
url="https://github.com/elementary/${gitname}"
license=('GPL3')
groups=('pantheon-unstable')
depends=('cairo' 'clutter' 'clutter-gtk' 'evolution-data-server' 'folks'
         'gdk-pixbuf2' 'geocode-glib' 'glib2' 'glibc' 'gtk3' 'libchamplain'
         'libgee' 'libical' 'libnotify' 'libsoup'
         'libgranite.so')
makedepends=('cmake' 'git' 'granite-git' 'intltool' 'vala')
provides=("${auxname}")
conflicts=('maya-calendar' "${auxname}")
replaces=('maya-calendar-bzr')
source=("${auxname}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
    cd "${auxname}"
    ( set -o pipefail
        git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
        printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
    )
}

build() {
    cd "${auxname}/"
    meson . _build --prefix=/usr
    ninja -C _build
}

package() {
    cd "${auxname}/"
    DESTDIR="${pkgdir}" ninja -C _build install
}
