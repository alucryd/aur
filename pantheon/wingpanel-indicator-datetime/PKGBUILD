# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>

pkgname=wingpanel-indicator-datetime
pkgver=2.0.2
pkgrel=1
pkgdesc='Date & Time indicator for Wingpanel'
arch=('i686' 'x86_64')
url='https://github.com/elementary/wingpanel-indicator-datetime'
license=('GPL3')
groups=('pantheon')
depends=('cairo' 'evolution-data-server' 'glib2' 'glibc' 'gtk3' 'libgee'
         'libical' 'libgranite.so' 'libwingpanel-2.0.so')
makedepends=('cmake' 'vala' 'wingpanel')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/elementary/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('d9723ccc6dc8d346ff7caad7defb01a9b7dae2d4958fd1576a23a25813431c47ee8edb3aa636f88a81ce099c2bc0107b11c2cb94e83eb665adf90694a8f8aecb')

build() {
  cd ${pkgname}-${pkgver}
  mkdir -p build
  cd build

  cmake .. \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib'
  make
}

package() {
  cd ${pkgname}-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
