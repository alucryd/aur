# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: sh4nks <sh4nks7@gmail.com

pkgname=lightdm-pantheon-greeter-bzr
pkgver=r261
pkgrel=1
pkgdesc='Pantheon greeter for LightDM'
arch=('i686' 'x86_64')
url='https://launchpad.net/pantheon-greeter'
license=('GPL')
depends=('clutter-gtk' 'granite' 'libindicator-gtk3' 'lightdm')
makedepends=('bzr' 'cmake' 'vala')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
install="${pkgname%-*}.install"
source=("${pkgname%-*}::bzr+http://bazaar.launchpad.net/~teemperor/pantheon-greeter/greeter-0.3/"
        'desktop.patch')
sha256sums=('SKIP'
            'bfb5fa8b7ca911509603b7b69fc5cc82082f90f192e8ac8d961e16500a43b6c4')

pkgver() {
  cd ${pkgname%-*}

  printf "r%s" "$(bzr revno)"
}

prepare() {
  cd ${pkgname%-*}

  patch -Np0 -i ../desktop.patch
}

build(){
  cd ${pkgname%-*}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX='/usr' -DGSETTINGS_COMPILE='OFF'
  make
}

package(){
  cd ${pkgname%-*}/build

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/usr/bin
  mv "${pkgdir}"/usr/{share/xgreeters/,bin/lightdm-}pantheon-greeter
  mv "${pkgdir}"/usr/share/xgreeters/{,lightdm-}pantheon-greeter.desktop
}

# vim: ts=2 sw=2 et:
