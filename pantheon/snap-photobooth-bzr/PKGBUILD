# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=snap-photobooth-bzr
pkgver=r216
pkgrel=1
pkgdesc='Snap webcam viewer'
arch=('i686' 'x86_64')
url='https://launchpad.net/snap-elementary'
license=('GPL3')
depends=('cheese' 'granite' 'gstreamer0.10-base-plugins')
makedepends=('bzr' 'cmake' 'vala')
install="${pkgname%-*}.install"
source=("${pkgname%-*}::bzr+lp:snap-elementary")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  printf "r%s" "$(bzr revno)"
}

prepare() {
  cd ${pkgname%-*}

  sed -i 's|libgnome-desktop/gnome-desktop-utils.h,||g
          s|libgnome-desktop/gnome-rr-labeler.h,||g' vapi/gnome-desktop-3.0.vapi
}

build() {
  cd ${pkgname%-*}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_BUILD_TYPE='Release' -DCMAKE_INSTALL_PREFIX='/usr' -DGSETTINGS_COMPILE='OFF'
  make
}

package() {
  cd ${pkgname%-*}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
