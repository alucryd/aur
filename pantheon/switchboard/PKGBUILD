# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>
# Contributor: dserban <dserban01@yahoo.com>

pkgname=switchboard
pkgver=2.3.0
pkgrel=1
pkgdesc='The Pantheon Control Center'
arch=('i686' 'x86_64')
url='https://github.com/elementary/switchboard'
license=('GPL3')
groups=('pantheon')
depends=('clutter-gtk' 'glib2' 'glibc' 'gtk3' 'libgee' 'libgranite.so')
makedepends=('cmake' 'intltool' 'vala')
optdepends=('switchboard-plug-about: About plug'
            'switchboard-plug-applications: Applications plug'
            'switchboard-plug-datetime: Date & Time plug'
            'switchboard-plug-desktop: Desktop plug'
            'switchboard-plug-display: Display plug'
            'switchboard-plug-elementary-tweaks: Elementary Tweaks plug'
            'switchboard-plug-keyboard: Keyboard plug'
            'switchboard-plug-locale: Locale plug'
            'switchboard-plug-network: Network plug'
            'switchboard-plug-notifications: Notifications plug'
            'switchboard-plug-power: Power plug'
            'switchboard-plug-security-privacy: Security & Privacy plug')
provides=('libswitchboard-2.0.so')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/elementary/switchboard/archive/${pkgver}.tar.gz")
sha512sums=('2ce0224ba2a6c3d46738d6fa2e94d2088bbcb023ca4398b7614960835eb9c9956077a18ec08f2c6749c4786c54ad2ff97e112cfc7d7aff0db97054229da7ad6c')

build() {
  mkdir -p build
  cd build

  cmake ../switchboard-${pkgver} \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib' \
    -DGSETTINGS_COMPILE='OFF' \
    -DUSE_UNITY='OFF'
  make
}

package() {
  make DESTDIR="${pkgdir}" -C build install
}

# vim: ts=2 sw=2 et:
