# Maintainer: alucryd <alucryd at gmail dot com>

pkgname=pantheon-calculator-bzr
pkgver=18
pkgrel=1
pkgdesc='A tiny, simple calculator written in GTK+ and Vala'
arch=('i686' 'x86_64')
url="https://launchpad.net/${pkgname%-*}"
license=('GPL3')
depends=('granite')
makedepends=('bzr' 'cmake' 'vala')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
install="${pkgname%-*}.install"
source=("bzr+lp:${pkgname%-*}")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  bzr revno
}

build() {
  cd ${pkgname%-*}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_BUILD_TYPE='Release' -DCMAKE_INSTALL_PREFIX='/usr'
  make
}

package() {
  cd ${pkgname%-*}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
