# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>

pkgname=noise-bzr
pkgver=r1654
pkgrel=1
pkgdesc='The Pantheon Music Player'
arch=('i686' 'x86_64')
url='https://launchpad.net/noise'
license=('GPL3')
depends=('desktop-file-utils' 'granite-bzr' 'gst-plugins-base-libs' 'json-glib' 'libgpod' 'libnotify' 'libpeas' 'libsoup' 'sqlheavy' 'taglib')
makedepends=('bzr' 'cmake' 'vala')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
install="${pkgname}.install"
source=('bzr+lp:noise'
        'mesa-demos-conflict.patch')
sha256sums=('SKIP'
            '131f11c0b80accab0eca91345f5638c44ea5207043fc45c52332294f06f40cf6')

pkgver() {
  cd ${pkgname%-*}

  printf "r%s" "$(bzr revno)"
}

prepare() {
  cd ${pkgname%-*}

  patch -Np1 -i ../mesa-demos-conflict.patch
  sed 's|\.\./||; s|${CMAKE_INSTALL_PREFIX}/${PLUGIN_DIR_UNPREFIXED}|${PLUGIN_DIR_UNPREFIXED}|' -i CMakeLists.txt
}

build() {
  cd ${pkgname%-*}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_{LIBDIR='/usr/lib',PREFIX='/usr'} \
           -DGSETTINGS_COMPILE='OFF'
  make -j1
}

package() {
  cd ${pkgname%-*}/build

  make -j1 DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
