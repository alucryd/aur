# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>

pkgname=noise-bzr
pkgver=r1726
pkgrel=1
pkgdesc='The Pantheon Music Player'
arch=('i686' 'x86_64')
url='https://launchpad.net/noise'
license=('GPL3')
depends=('desktop-file-utils' 'granite-bzr' 'gst-plugins-base-libs' 'libgpod'
         'libnotify' 'libpeas' 'sqlheavy' 'taglib')
makedepends=('bzr' 'cmake' 'vala')
optdepends=('gst-plugins-base: "Base" plugin libraries'
            'gst-plugins-good: "Good" plugin libraries'
            'gst-plugins-bad: "Bad" plugin libraries'
            'gst-plugins-ugly: "Ugly" plugin libraries'
            'gst-libav: Libav plugin')
provides=('noise')
conflicts=('noise')
install='noise.install'
source=('bzr+lp:noise'
        'mesa-demos-conflict.patch')
sha256sums=('SKIP'
            '5762f448021d326caa628609390074732b38323df894b4173cc2dc799c3d32bc')

pkgver() {
  cd noise

  printf "r%s" "$(bzr revno)"
}

prepare() {
  cd noise

  patch -Np1 -i ../mesa-demos-conflict.patch
  sed 's|\.\./||; s|${CMAKE_INSTALL_PREFIX}/${PLUGIN_DIR_UNPREFIXED}|${PLUGIN_DIR_UNPREFIXED}|' -i CMakeLists.txt
}

build() {
  cd noise

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib' \
    -DGSETTINGS_COMPILE='FALSE'
  make -j1
}

package() {
  cd noise/build

  make -j1 DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
