# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>

pkgname=eidete-bzr
pkgver=r165
pkgrel=1
pkgdesc='Simple screencaster for the elementary project'
arch=('i686' 'x86_64')
url='https://launchpad.net/eidete'
license=('GPL3')
depends=('granite' 'gstreamer0.10-base' 'libwnck3')
makedepends=('bzr' 'cmake' 'vala')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
install="${pkgname%-*}.install"
source=('bzr+lp:eidete')
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  printf "r%s" "$(bzr revno)"
}

build() {
  cd ${pkgname%-*}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_BUILD_TYPE='Release' -DCMAKE_INSTALL_PREFIX='/usr' -DCMAKE_EXE_LINKER_FLAGS='-lX11'
  make
}

package() {
  cd ${pkgname%-*}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
