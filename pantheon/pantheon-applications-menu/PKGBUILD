# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>

pkgname=pantheon-applications-menu
_pkgname=applications-menu
pkgver=2.1.3.p1
pkgrel=1
pkgdesc='The Pantheon Application Menu'
arch=('i686' 'x86_64')
url='https://github.com/elementary/applications-menu'
license=('GPL3')
groups=('pantheon')
depends=('appstream' 'gdk-pixbuf2' 'glib2' 'glibc' 'gnome-menus' 'gtk3'
         'json-glib' 'libgee' 'libsoup' 'plank' 'zeitgeist'
         'libgranite.so' 'libswitchboard-2.0.so' 'libwingpanel-2.0.so')
makedepends=('cmake' 'granite' 'switchboard' 'vala' 'wingpanel')
conflicts=('slingshot-launcher')
replaces=('slingshot-launcher-bzr')
_commit='f2a9e64796c8fc9c5619d37f72867797717b7d04'
source=("${pkgname}-${_commit}.tar.gz::https://github.com/elementary/${_pkgname}/archive/${_commit}.tar.gz")
#source=("${pkgname}-${pkgver}.tar.gz::https://github.com/elementary/${_pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('1901b2f16cd615791ba9f32e1986f2d3c253d32a7b67a3f73c87cb7cc2a2a89e3dd4dbf07309931ddae6748ac64c306a33147dbe88609652b2ecc30792b1ae1f')

build() {
  #cd "${_pkgname}-${pkgver}"
  cd "${_pkgname}-${_commit}"

  mkdir -p build
  cd build

  cmake .. \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib' \
    -DGSETTINGS_COMPILE='OFF' \
    -DUSE_UNITY='OFF'
  make
}

package() {
  #cd "${_pkgname}-${pkgver}/build"
  cd "${_pkgname}-${_commit}/build"
  make DESTDIR="${pkgdir}" install
}
