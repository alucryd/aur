# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel-indicator-session-bzr
pkgver=r23
pkgrel=1
pkgdesc='Session indicator for Wingpanel'
arch=('i686' 'x86_64')
url='https://launchpad.net/wingpanel-indicator-session'
license=('GPL3')
groups=('pantheon-unstable')
depends=('wingpanel-0.4.x-bzr' 'accountsservice')
makedepends=('bzr' 'cmake' 'vala')
provides=('wingpanel-indicator-session')
conflicts=('wingpanel-indicator-session')
install='wingpanel.install'
source=('bzr+lp:wingpanel-indicator-session')
sha256sums=('SKIP')

pkgver() {
  cd wingpanel-indicator-session

  echo "r$(bzr revno)"
}

prepare() {
  cd wingpanel-indicator-session

  mkdir vapi && cd vapi
  cp /usr/share/vala-0.28/vapi/{gio,gobject}-2.0.vapi .
}

build() {
  cd wingpanel-indicator-session

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib'
  make
}

package() {
  cd wingpanel-indicator-session/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
