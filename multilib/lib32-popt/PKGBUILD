# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: jospehgbr <rafael.f.f1@gmail.com>
# Contributor: Michael Lass <bevan@bi-co.net>
# Contributor: coderoar <coderoar@gmail.com>
# Contributor: webjdm <web.jdm@gmail.com>

pkgname=lib32-popt
pkgver=1.16
pkgrel=6
pkgdesc='A commandline option parser'
arch=('x86_64')
url='http://rpm5.org'
license=('custom')
depends=('lib32-glibc' "popt=${pkgver}")
makedepends=('gcc-multilib')
source=("http://rpm5.org/files/popt/popt-${pkgver}.tar.gz")
sha256sums=('e728ed296fe9f069a0e005003c3d6b2dde3d9cad453422a10d6558616d304cc8')

build() {
  cd popt-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32'
  make
}

package() {
  cd popt-${pkgver}

  make DESTDIR="${pkgdir}" pkgconfigdir='/usr/lib32/pkgconfig' install
  rm -rf "${pkgdir}"/usr/{include,share}

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s popt "${pkgdir}"/usr/share/licenses/lib32-popt
}

# vim: ts=2 sw=2 et:
